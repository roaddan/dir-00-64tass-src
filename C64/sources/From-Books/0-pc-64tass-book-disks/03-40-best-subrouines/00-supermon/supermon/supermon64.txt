
; 64tass Turbo Assembler Macro V1.54.1900 listing file
; 64tass -C -m -a -I /Users/Locals/C64/00-usbkey32go/d-00-64tass-src/lib -i -L supermon64.txt -o supermon64.prg supermon64.asm
; Fri Nov 14 00:52:51 2025

;Offset	;Hex		;Source

;******  Processing input file: supermon64.asm

=$c1			tmp0    = $c1               ; used to return input, often holds end address
=$c3			tmp2    = $c3               ; usually holds start address
=$90			satus   = $90               ; kernal i/o status word
=$b7			fnlen   = $b7               ; length of current filename
=$b9			sadd    = $b9               ; current secondary address (official name sa)
=$ba			fa      = $ba               ; current device number
=$bb			fnadr   = $bb               ; pointer to current filename
=$c6			ndx     = $c6               ; number of characters in keyboard buffer
=$0277			keyd    = $0277             ; keyboard buffer
=$0316			bkvec   = $0316             ; brk instruction vector (official name cbinv)
>0100			acmd    .fill 1             ; addressing command
>0101			length  .fill 1             ; length of operand
>0102			mnemw   .fill 3             ; 3 letter mnemonic buffer
>0105			savx    .fill 1             ; 1 byte temp storage, often to save x register
>0106			opcode  .fill 1             ; current opcode for assembler/disassembler
>0107			upflg   .fill 1             ; flag: count up (bit 7 clear) or down (bit 7 set)
>0108			digcnt  .fill 1             ; digit count
>0109			indig   .fill 1             ; numeric value of single digit
>010a			numbit  .fill 1             ; numeric base of input
>010b			stash   .fill 2             ; 2-byte temp storage
>010d			u0aa0   .fill 10            ; work buffer
=$0117			u0aae   =*                  ; end of work buffer
>0117			stage   .fill 30            ; staging buffer for filename, search, etc.
=$0135			estage  =*                  ; end of staging buffer
>0200			inbuff  .fill 40            ; 40-character input buffer
=$0228			endin   =*                  ; end of input buffer
>0228			pch     .fill 1             ; program counter high byte
>0229			pcl     .fill 1             ; program counter low byte
>022a			sr      .fill 1             ; status register
>022b			acc     .fill 1             ; accumulator
>022c			xr      .fill 1             ; x register
>022d			yr      .fill 1             ; y register
>022e			sp      .fill 1             ; stack pointer
>022f			store   .fill 2             ; 2-byte temp storage
>0231			chrpnt  .fill 1             ; current position in input buffer
>0232			savy    .fill 1             ; temp storage, often to save y register
>0233			u9f     .fill 1             ; index into assembler work buffer
=$ff90			setmsg  = $ff90             ; set kernel message control flag
=$ff93			second  = $ff93             ; set secondary address after listen
=$ff96			tksa    = $ff96             ; send secondary address after talk
=$ffb1			listen  = $ffb1             ; command serial bus device to listen
=$ffb4			talk    = $ffb4             ; command serial bus device to talk
=$ffba			setlfs  = $ffba             ; set logical file parameters
=$ffbd			setnam  = $ffbd             ; set filename
=$ffa5			acptr   = $ffa5             ; input byte from serial bus
=$ffa8			ciout   = $ffa8             ; output byte to serial bus
=$ffab			untlk   = $ffab             ; command serial bus device to untalk
=$ffae			unlsn   = $ffae             ; command serial bus device to unlisten
=$ffc6			chkin   = $ffc6             ; define input channel
=$ffcc			clrchn  = $ffcc             ; restore default devices
=$ffcf			input   = $ffcf             ; input a character (official name chrin)
=$ffd2			chrout  = $ffd2             ; output a character
=$ffd5			load    = $ffd5             ; load from device
=$ffd8			save    = $ffd8             ; save to device
=$ffe1			stop    = $ffe1             ; check the stop key
=$ffe4			getin   = $ffe4             ; get a character
=$c000			org     = $c000
.c000	a0 20		super   ldy #msg4-msgbas    ; display "..sys "
.c002	20 84 c9	        jsr sndmsg
.c005	ad e5 ca	        lda supad           ; store entry point address in tmp0
.c008	85 c1		        sta tmp0
.c00a	ad e6 ca	        lda supad+1
.c00d	85 c2		        sta tmp0+1
.c00f	20 0e c8	        jsr cvtdec          ; convert address to decimal
.c012	a9 00		        lda #0
.c014	a2 06		        ldx #6
.c016	a0 03		        ldy #3
.c018	20 48 c8	        jsr nmprnt          ; print entry point address
.c01b	20 dd c6	        jsr crlf
.c01e	ad e3 ca	        lda linkad          ; set brk vector
.c021	8d 16 03	        sta bkvec
.c024	ad e4 ca	        lda linkad+1
.c027	8d 17 03	        sta bkvec+1
.c02a	a9 80		        lda #$80            ; disable kernel control messages
.c02c	20 90 ff	        jsr setmsg          ; and enable error messages
.c02f	00		        brk
.c030	a2 05		break   ldx #$05            ; pull registers off the stack
.c032	68		bstack  pla                 ; order: y,x,a,sr,pcl,pch
.c033	9d 28 02	        sta pch,x           ; store in memory
.c036	ca		        dex
.c037	10 f9		        bpl bstack
.c039	d8		        cld                 ; disable bcd mode
.c03a	ba		        tsx                 ; store stack pointer in memory
.c03b	8e 2e 02	        stx sp
.c03e	58		        cli                 ; enable interupts
.c03f	a0 00		dsplyr  ldy #msg2-msgbas    ; display headers
.c041	20 71 c9	        jsr sndclr
.c044	a9 3b		        lda #$3b            ; prefix registers with "; " to allow editing
.c046	20 d2 ff	        jsr chrout
.c049	a9 20		        lda #$20
.c04b	20 d2 ff	        jsr chrout
.c04e	ad 28 02	        lda pch             ; print 2-byte program counter
.c051	20 f6 c6	        jsr wrtwo
.c054	a0 01		        ldy #1              ; start 1 byte after pc high byte
.c056	b9 28 02	disj    lda pch,y           ; loop through rest of the registers
.c059	20 d2 c6	        jsr wrbyte          ; print 1-byte register value
.c05c	c8		        iny
.c05d	c0 07		        cpy #7              ; there are a total of 5 registers to print
.c05f	90 f5		        bcc disj
.c061	20 dd c6	strt    jsr crlf            ; new line
.c064	a2 00		        ldx #0              ; point at start of input buffer
.c066	8e 31 02	        stx chrpnt
.c069	20 cf ff	smove   jsr input           ; chrin kernal call to input a character
.c06c	9d 00 02	        sta inbuff,x        ; store in input buffer
.c06f	e8		        inx
.c070	e0 28		        cpx #endin-inbuff   ; error if buffer is full
.c072	b0 1c		        bcs error
.c074	c9 0d		        cmp #$0d            ; keep reading until cr
.c076	d0 f1		        bne smove
.c078	a9 00		        lda #0              ; null-terminate input buffer
.c07a	9d ff 01	        sta inbuff-1,x      ; (replacing the cr)
.c07d	20 1e c7	st1     jsr getchr          ; get a character from the buffer
.c080	f0 df		        beq strt            ; start over if buffer is empty
.c082	c9 20		        cmp #$20            ; skip leading spaces
.c084	f0 f7		        beq st1
.c086	a2 16		s0      ldx #keytop-keyw    ; loop through valid command characters
.c088	dd a7 ca	s1      cmp keyw,x          ; see if input character matches
.c08b	f0 0b		        beq s2              ; command matched, dispatch it
.c08d	ca		        dex                 ; no match, check next command
.c08e	10 f8		        bpl s1              ; keep trying until we've checked them all
.c090	a0 1e		error   ldy #msg3-msgbas    ; display "?" to indicate error and go to new line
.c092	20 84 c9	        jsr sndmsg
.c095	4c 61 c0	        jmp strt            ; back to main loop
.c098	e0 13		s2      cpx #$13            ; last 3 commands in table are load/save/validate
.c09a	b0 12		        bcs lsv             ;   which are handled by the same subroutine
.c09c	e0 0f		        cpx #$0f            ; next 4 commands are base conversions
.c09e	b0 14		        bcs cnvlnk          ;   which are handled by the same subroutine
.c0a0	8a		        txa                 ; remaining commands dispatch through vector table
.c0a1	0a		        asl a               ; multiply index of command by 2
.c0a2	aa		        tax                 ;   since table contains 2-byte addresses
.c0a3	bd be ca	        lda kaddr+1,x       ; push address from vector table onto stack
.c0a6	48		        pha                 ;   so that the rts from getpar will jump there
.c0a7	bd bd ca	        lda kaddr,x
.c0aa	48		        pha
.c0ab	4c ff c5	        jmp getpar          ; get the first parameter for the command
.c0ae	8d 32 02	lsv     sta savy            ; handle load/save/validate
.c0b1	4c 81 c2	        jmp ld
.c0b4	4c c3 c7	cnvlnk  jmp convrt          ; handle base conversion
.c0b7	6c 02 a0	exit    jmp ($a002)         ; jump to warm-start vector to reinitialize basic
.c0ba	b0 08		dsplym  bcs dspm11          ; start from previous end addr if no address given
.c0bc	20 38 c7	        jsr copy12          ; save start address in tmp2
.c0bf	20 ff c5	        jsr getpar          ; get end address in tmp0
.c0c2	90 06		        bcc dsmnew          ; did user specify one?
.c0c4	a9 0b		dspm11  lda #$0b            ; if not, show 12 lines by default
.c0c6	85 c1		        sta tmp0
.c0c8	d0 0e		        bne dspbyt          ; always true, but bne uses 1 byte less than jmp
.c0ca	20 41 c7	dsmnew  jsr sub12           ; end addr given, calc bytes between start and end
.c0cd	90 1e		        bcc merror          ; error if start is after end
.c0cf	a2 03		        ldx #3              ; divide by 8 (shift right 3 times)
.c0d1	46 c2		dspm01  lsr tmp0+1
.c0d3	66 c1		        ror tmp0
.c0d5	ca		        dex
.c0d6	d0 f9		        bne dspm01
.c0d8	20 e1 ff	dspbyt  jsr stop            ; check for stop key
.c0db	f0 0d		        beq dspmx           ; exit early if pressed
.c0dd	20 5d c1	        jsr dispmem         ; display 1 line containing 8 bytes
.c0e0	a9 08		        lda #8              ; increase start address by 8 bytes
.c0e2	20 77 c7	        jsr bumpad2
.c0e5	20 4f c7	        jsr suba1           ; decrement line counter
.c0e8	b0 ee		        bcs dspbyt          ; show another line until it's < 0
.c0ea	4c 61 c0	dspmx   jmp strt            ; back to main loop
.c0ed	4c 90 c0	merror  jmp error           ; handle error
.c0f0	20 8f c7	altr    jsr copy1p          ; store first parameter in pc
.c0f3	a0 00		        ldy #0              ; init counter
.c0f5	20 ff c5	altr1   jsr getpar          ; get value for next register
.c0f8	b0 0a		        bcs altrx           ; exit early if no more values given
.c0fa	a5 c1		        lda tmp0            ; store in memory, offset from sr
.c0fc	99 2a 02	        sta sr,y            ; these locations will be transferred to the
.c0ff	c8		        iny                 ;   actual registers before exiting the monitor
.c100	c0 05		        cpy #$05            ; have we updated all 5 yet?
.c102	90 f1		        bcc altr1           ; if not, get next
.c104	4c 61 c0	altrx   jmp strt            ; back to main loop
.c107	b0 13		altm    bcs altmx           ; exit if no parameter provided
.c109	20 38 c7	        jsr copy12          ; copy parameter to start address
.c10c	a0 00		        ldy #0
.c10e	20 ff c5	altm1   jsr getpar          ; get value for next byte of memory
.c111	b0 09		        bcs altmx           ; if none given, exit early
.c113	a5 c1		        lda tmp0            ; poke value into memory at start address + y
.c115	91 c3		        sta (tmp2),y
.c117	c8		        iny                 ; next byte
.c118	c0 08		        cpy #8              ; have we read 8 bytes yet?
.c11a	90 f2		        bcc altm1           ; if not, read the next one
.c11c	a9 91		altmx   lda #$91            ; move cursor up
.c11e	20 d2 ff	        jsr chrout
.c121	20 5d c1	        jsr dispmem         ; re-display line to make ascii match hex
.c124	4c 61 c0	        jmp strt            ; back to main loop
.c127	ae 2e 02	goto    ldx sp              ; load stack pointer from memory
.c12a	9a		        txs                 ; save in sp register
.c12b	20 8f c7	goto2   jsr copy1p          ; copy provided address to pc
.c12e	78		        sei                 ; disable interrupts
.c12f	ad 28 02	        lda pch             ; push pc high byte on stack
.c132	48		        pha
.c133	ad 29 02	        lda pcl             ; push pc low byte on stack
.c136	48		        pha
.c137	ad 2a 02	        lda sr              ; push status byte on stack
.c13a	48		        pha
.c13b	ad 2b 02	        lda acc             ; load accumulator from memory
.c13e	ae 2c 02	        ldx xr              ; load x from memory
.c141	ac 2d 02	        ldy yr              ; load y from memory
.c144	40		        rti                 ; return from interrupt (pops pc and sr)
.c145	ae 2e 02	jsub    ldx sp              ; load stack pointer from memory
.c148	9a		        txs                 ; save value in sp register
.c149	20 2b c1	        jsr goto2           ; same as goto command
.c14c	8c 2d 02	        sty yr              ; save y to memory
.c14f	8e 2c 02	        stx xr              ; save x to memory
.c152	8d 2b 02	        sta acc             ; save accumulator to memory
.c155	08		        php                 ; push processor status on stack
.c156	68		        pla                 ; pull processor status into a
.c157	8d 2a 02	        sta sr              ; save processor status to memory
.c15a	4c 3f c0	        jmp dsplyr          ; display registers
.c15d	20 dd c6	dispmem jsr crlf            ; new line
.c160	a9 3e		        lda #">"            ; prefix > so memory can be edited in place
.c162	20 d2 ff	        jsr chrout
.c165	20 c8 c6	        jsr showad          ; show address of first byte on line
.c168	a0 00		        ldy #0
.c16a	f0 03		        beq dmemgo          ; showad already printed a space after the address
.c16c	20 d5 c6	dmemlp  jsr space           ; print space between bytes
.c16f	b1 c3		dmemgo  lda (tmp2),y        ; load byte from start address + y
.c171	20 f6 c6	        jsr wrtwo           ; output hex digits for byte
.c174	c8		        iny                 ; next byte
.c175	c0 08		        cpy #8              ; have we output 8 bytes yet?
.c177	90 f3		        bcc dmemlp          ; if not, output next byte
.c179	a0 26		        ldy #msg5-msgbas    ; if so, output : and turn on reverse video
.c17b	20 84 c9	        jsr sndmsg          ;   before displaying ascii representation
.c17e	a0 00		        ldy #0              ; back to first byte in line
.c180	b1 c3		dchar   lda (tmp2),y        ; load byte at start address + y
.c182	aa		        tax                 ; stash in x
.c183	29 bf		        and #$bf            ; clear 6th bit
.c185	c9 22		        cmp #$22            ; is it a quote (")?
.c187	f0 08		        beq ddot            ; if so, print . instead
.c189	8a		        txa                 ; if not, restore character
.c18a	29 7f		        and #$7f            ; clear top bit
.c18c	c9 20		        cmp #$20            ; is it a printable character (>= $20)?
.c18e	8a		        txa                 ; restore character
.c18f	b0 02		        bcs dchrok          ; if printable, output character
.c191	a9 2e		ddot    lda #$2e            ; if not, output '.' instaed
.c193	20 d2 ff	dchrok  jsr chrout
.c196	c8		        iny                 ; next byte
.c197	c0 08		        cpy #8              ; have we output 8 bytes yet?
.c199	90 e5		        bcc dchar           ; if not, output next byte
.c19b	60		        rts
.c19c	a9 00		compar  lda #0              ; bit 7 clear signals compare
>c19e	2c		        .byte $2c           ; absolute bit opcode consumes next word (lda #$80)
.c19f	a9 80		trans   lda #$80            ; bit 7 set signals transfer
.c1a1	8d 32 02	        sta savy            ; save compare/transfer flag in savy
.c1a4	a9 00		        lda #0              ; assume we're counting up (bit 7 clear)
.c1a6	8d 07 01	        sta upflg           ; save direction flag
.c1a9	20 9c c7	        jsr getdif          ; get two addresses and calculate difference
.c1ac	b0 05		        bcs terror          ; carry set indicates error
.c1ae	20 ff c5	        jsr getpar          ; get destination address in tmp0
.c1b1	90 03		        bcc tokay           ; carry set indicates error
.c1b3	4c 90 c0	terror  jmp error           ; handle error
.c1b6	2c 32 02	tokay   bit savy            ; transfer or compare?
.c1b9	10 27		        bpl compar1         ; high bit clear indicates compare
.c1bb	a5 c3		        lda tmp2            ; if it's a transfer, we must take steps
.c1bd	c5 c1		        cmp tmp0            ;   to avoid overwriting the source bytes before
.c1bf	a5 c4		        lda tmp2+1          ;   they have been transferred
.c1c1	e5 c2		        sbc tmp0+1          ; compare source (tmp2) to destination (tmp0)
.c1c3	b0 1d		        bcs compar1         ; and count up if source is before than desitnation
.c1c5	ad 2f 02	        lda store           ; otherwise, start at end and count down...
.c1c8	65 c1		        adc tmp0            ; add length (store) to desintation (tmp0)
.c1ca	85 c1		        sta tmp0            ; to calculate end of destination
.c1cc	ad 30 02	        lda store+1
.c1cf	65 c2		        adc tmp0+1
.c1d1	85 c2		        sta tmp0+1
.c1d3	a2 01		        ldx #1              ; change source pointer from beginning to end
.c1d5	bd 0b 01	tdown   lda stash,x         ; tmp2 = source end (stash)
.c1d8	95 c3		        sta tmp2,x
.c1da	ca		        dex
.c1db	10 f8		        bpl tdown
.c1dd	a9 80		        lda #$80            ; high bit set in upflg means count down
.c1df	8d 07 01	        sta upflg
.c1e2	20 dd c6	compar1 jsr crlf            ; new line
.c1e5	a0 00		        ldy #0              ; no offset from pointer
.c1e7	20 e1 ff	tcloop  jsr stop            ; check for stop key
.c1ea	f0 31		        beq texit           ; exit if pressed
.c1ec	b1 c3		        lda (tmp2),y        ; load byte from source
.c1ee	2c 32 02	        bit savy            ; transfer or compare?
.c1f1	10 02		        bpl compar2         ; skip store if comparing
.c1f3	91 c1		        sta (tmp0),y        ; otherwise, store in destination
.c1f5	d1 c1		compar2 cmp (tmp0),y        ; compare to destination
.c1f7	f0 03		        beq tmvad           ; don't show address if equal
.c1f9	20 c8 c6	        jsr showad          ; show address
.c1fc	2c 07 01	tmvad   bit upflg           ; counting up or down?
.c1ff	30 0b		        bmi tdecad          ; high bit set means we're counting down
.c201	e6 c1		        inc tmp0            ; increment destination low byte
.c203	d0 10		        bne tincok
.c205	e6 c2		        inc tmp0+1          ; carry to high byte if necessary
.c207	d0 0c		        bne tincok
.c209	4c 90 c0	        jmp error           ; error if high byte overflowed
.c20c	20 4f c7	tdecad  jsr suba1           ; decrement destination (tmp0)
.c20f	20 81 c7	        jsr sub21           ; decrement source (tmp2)
.c212	4c 18 c2	        jmp tmor
.c215	20 75 c7	tincok  jsr adda2           ; increment source (tmp2)
.c218	20 63 c7	tmor    jsr sub13           ; decrement length
.c21b	b0 ca		        bcs tcloop          ; loop until length is 0
.c21d	4c 61 c0	texit   jmp strt            ; back to main loop
.c220	20 9c c7	hunt    jsr getdif          ; get start (tmp2) and end (tmp0) of haystack
.c223	b0 59		        bcs herror          ; carry indicates error
.c225	a0 00		        ldy #0
.c227	20 1e c7	        jsr getchr          ; get a single character
.c22a	c9 27		        cmp #"'"            ; is it a single quote?
.c22c	d0 16		        bne nostrh          ; if not, input needle as hex bytes
.c22e	20 1e c7	        jsr getchr          ; if so, input needle as string
.c231	c9 00		        cmp #0
.c233	f0 49		        beq herror          ; error if needle isn't at least one byte
.c235	99 17 01	hpar    sta stage,y         ; save char in staging area
.c238	c8		        iny
.c239	20 1e c7	        jsr getchr          ; get another char
.c23c	f0 18		        beq htgo            ; if it's null start searching
.c23e	c0 1e		        cpy #estage-stage   ; have we filled up the needle staging area?
.c240	d0 f3		        bne hpar            ; if not, get another character
.c242	f0 12		        beq htgo            ; if so, start searching
.c244	20 fc c5	nostrh  jsr rdpar           ; read hex bytes if string not indicated
.c247	a5 c1		hlp     lda tmp0            ; save last read byte in staging area
.c249	99 17 01	        sta stage,y
.c24c	c8		        iny                 ; get another hex byte
.c24d	20 ff c5	        jsr getpar
.c250	b0 04		        bcs htgo            ; if there is none, start searching
.c252	c0 1e		        cpy #estage-stage   ; have we filled up the needle staging area?
.c254	d0 f1		        bne hlp             ; if not, get another byte
.c256	8c 32 02	htgo    sty savy            ; save length of needle
.c259	20 dd c6	        jsr crlf            ; new line
.c25c	a0 00		hscan   ldy #0
.c25e	b1 c3		hlp3    lda (tmp2),y        ; get first byte in haystack
.c260	d9 17 01	        cmp stage,y         ; compare it to first byte of needle
.c263	d0 09		        bne hnoft           ; if it doesn't match, we haven't found anything
.c265	c8		        iny                 ; if it does, check the next byte
.c266	cc 32 02	        cpy savy            ; have we reached the end of the needle?
.c269	d0 f3		        bne hlp3            ; if not, keep comparing bytes
.c26b	20 c8 c6	        jsr showad          ; match found, show address
.c26e	20 e1 ff	hnoft   jsr stop            ; no match, check for stop key
.c271	f0 08		        beq hexit           ; exit prematurely if pressed
.c273	20 75 c7	        jsr adda2           ; increment haystack pointer
.c276	20 63 c7	        jsr sub13           ; decrement haystack length
.c279	b0 e1		        bcs hscan           ; still more haystack? keep searching
.c27b	4c 61 c0	hexit   jmp strt            ; back to main loop
.c27e	4c 90 c0	herror  jmp error           ; handle error
.c281	a0 01		ld      ldy #1              ; default to reading from tape, device #1
.c283	84 ba		        sty fa
.c285	84 b9		        sty sadd            ; default to secondary address #1
.c287	88		        dey
.c288	84 b7		        sty fnlen           ; start with an empty filename
.c28a	84 90		        sty satus           ; clear status
.c28c	a9 01		        lda #>stage         ; set filename pointer to staging buffer
.c28e	85 bc		        sta fnadr+1
.c290	a9 17		        lda #<stage
.c292	85 bb		        sta fnadr
.c294	20 1e c7	l1      jsr getchr          ; get a character
.c297	f0 59		        beq lshort          ; no filename given, try load or verify from tape
.c299	c9 20		        cmp #$20            ; skip leading spaces
.c29b	f0 f7		        beq l1
.c29d	c9 22		        cmp #$22            ; error if filename doesn't start with a quote
.c29f	d0 16		        bne lerror
.c2a1	ae 31 02	        ldx chrpnt          ; load current char pointer into index reg
.c2a4	bd 00 02	l3      lda inbuff,x        ; load current char from buffer to accumulator
.c2a7	f0 49		        beq lshort          ; no filename given, try load or verify from tape
.c2a9	e8		        inx                 ; next char
.c2aa	c9 22		        cmp #$22            ; is it a quote?
.c2ac	f0 0c		        beq l8              ; if so, we've reached the end of the filename
.c2ae	91 bb		        sta (fnadr),y       ; if not, save character in filename buffer
.c2b0	e6 b7		        inc fnlen           ; increment filename length
.c2b2	c8		        iny
.c2b3	c0 1e		        cpy #estage-stage   ; check whether buffer is full
.c2b5	90 ed		        bcc l3              ; if not, get another character
.c2b7	4c 90 c0	lerror  jmp error           ; if so, handle error
.c2ba	8e 31 02	l8      stx chrpnt          ; set character pointer to the current index
.c2bd	20 1e c7	        jsr getchr          ; eat separator between filename and device #
.c2c0	f0 30		        beq lshort          ; no separator, try to load or verify from tape
.c2c2	20 ff c5	        jsr getpar          ; get device number
.c2c5	b0 2b		        bcs lshort          ; no device # given, try load or verify from tape
.c2c7	a5 c1		        lda tmp0            ; set device number for kernal routines
.c2c9	85 ba		        sta fa
.c2cb	20 ff c5	        jsr getpar          ; get start address for load or save in tmp0
.c2ce	b0 22		        bcs lshort          ; no start address, try to load or verify
.c2d0	20 38 c7	        jsr copy12          ; transfer start address to tmp2
.c2d3	20 ff c5	        jsr getpar          ; get end address for save in tmp0
.c2d6	b0 3d		        bcs ldaddr          ; no end address, try to load to given start addr
.c2d8	20 dd c6	        jsr crlf            ; new line
.c2db	a6 c1		        ldx tmp0            ; put low byte of end address in x
.c2dd	a4 c2		        ldy tmp0+1          ; put high byte of end address in y
.c2df	ad 32 02	        lda savy            ; confirm that we're doing a save
.c2e2	c9 53		        cmp #"s"
.c2e4	d0 d1		        bne lerror          ; if not, error due to too many params
.c2e6	a9 00		        lda #0
.c2e8	85 b9		        sta sadd            ; set secondary address to 0
.c2ea	a9 c3		        lda #tmp2           ; put addr of zero-page pointer to data in a
.c2ec	20 d8 ff	        jsr save            ; call kernal save routine
.c2ef	4c 61 c0	lsvxit  jmp strt            ; back to mainloop
.c2f2	ad 32 02	lshort  lda savy            ; check which command we received
.c2f5	c9 56		        cmp #"v"
.c2f7	f0 06		        beq loadit          ; we're doing a verify so don't set a to 0
.c2f9	c9 4c		        cmp #"l"
.c2fb	d0 ba		        bne lerror          ; error due to not enough params for save
.c2fd	a9 00		        lda #0              ; 0 in a signals load, anything else is verify
.c2ff	20 d5 ff	loadit  jsr load            ; call kernal load routine
.c302	a5 90		        lda satus           ; get i/o status
.c304	29 10		        and #$10            ; check bit 5 for checksum error
.c306	f0 e7		        beq lsvxit          ; if no error go back to mainloop
.c308	ad 32 02	        lda savy            ; ?? not sure what these two lines are for...
.c30b	f0 aa		        beq lerror          ; ?? savy will never be 0, so why check?
.c30d	a0 28		        ldy #msg6-msgbas    ; display "error" if checksum didn't match
.c30f	20 84 c9	        jsr sndmsg
.c312	4c 61 c0	        jmp strt            ; back to mainloop
.c315	a6 c3		ldaddr  ldx tmp2            ; load address low byte in x
.c317	a4 c4		        ldy tmp2+1          ; load address high byte in y
.c319	a9 00		        lda #0              ; 0 in a signals load
.c31b	85 b9		        sta sadd            ; secondary addr 0 means load to addr in x and y
.c31d	f0 d3		        beq lshort          ; execute load
.c31f	20 9c c7	fill    jsr getdif          ; start in tmp2, end in stash, length in store
.c322	b0 59		        bcs aerror          ; carry set indicates error
.c324	20 ff c5	        jsr getpar          ; get value to fill in tmp0
.c327	b0 54		        bcs aerror          ; carry set indicates error
.c329	20 1e c7	        jsr getchr          ; any more characters triggers an error
.c32c	d0 4f		        bne aerror
.c32e	a0 00		        ldy #0              ; no offset
.c330	a5 c1		fillp   lda tmp0            ; load value to fill in accumulator
.c332	91 c3		        sta (tmp2),y        ; store fill value in current address
.c334	20 e1 ff	        jsr stop            ; check for stop key
.c337	f0 08		        beq fstart          ; if pressed, back to main loop
.c339	20 75 c7	        jsr adda2           ; increment address
.c33c	20 63 c7	        jsr sub13           ; decrement length
.c33f	b0 ef		        bcs fillp           ; keep going until length reaches 0
.c341	4c 61 c0	fstart  jmp strt            ; back to main loop
.c344	b0 37		assem   bcs aerror          ; error if no address given
.c346	20 38 c7	        jsr copy12          ; copy address to tmp2
.c349	a2 00		aget1   ldx #0
.c34b	8e 0e 01	        stx u0aa0+1         ; clear byte that mnemonic gets shifted into
.c34e	8e 08 01	        stx digcnt          ; clear digit count
.c351	20 1e c7	aget2   jsr getchr          ; get a char
.c354	d0 04		        bne almor           ; proceed if the character isn't null
.c356	e0 00		        cpx #0              ; it's null, have read a mnemonic yet?
.c358	f0 e7		        beq fstart          ; if not, silently go back to main loop
.c35a	c9 20		almor   cmp #$20            ; skip leading spaces
.c35c	f0 eb		        beq aget1
.c35e	9d 02 01	        sta mnemw,x         ; put character in mnemonic buffer
.c361	e8		        inx
.c362	e0 03		        cpx #3              ; have we read 3 characters yet?
.c364	d0 eb		        bne aget2           ; if not, get next character
.c366	ca		asqeez  dex                 ; move to previous char
.c367	30 17		        bmi aoprnd          ; if we're done with mnemonic, look for operand
.c369	bd 02 01	        lda mnemw,x         ; get current character
.c36c	38		        sec                 ; pack 3-letter mnemonic into 2 bytes (15 bits)
.c36d	e9 3f		        sbc #$3f            ; subtract $3f from ascii code so a-z = 2 to 27
.c36f	a0 05		        ldy #$05            ; letters now fit in 5 bits; shift them out
.c371	4a		ashift  lsr a               ;   into the first two bytes of the inst buffer
.c372	6e 0e 01	        ror u0aa0+1         ; catch the low bit from accumulator in right byte
.c375	6e 0d 01	        ror u0aa0           ; catch the low bit from right byte in left byte
.c378	88		        dey                 ; count down bits
.c379	d0 f6		        bne ashift          ; keep looping until we reach zero
.c37b	f0 e9		        beq asqeez          ; unconditional branch to handle next char
.c37d	4c 90 c0	aerror  jmp error           ; handle error
.c380	a2 02		aoprnd  ldx #2              ; mnemonic is in first two bytes so start at third
.c382	ad 08 01	ascan   lda digcnt          ; did we find address digits last time?
.c385	d0 2d		        bne aform1          ; if so, look for mode chars
.c387	20 27 c6	        jsr rdval           ; otherwise, look for an address
.c38a	f0 25		        beq aform0          ; we didn't find an address, look for characters
.c38c	b0 ef		        bcs aerror          ; carry flag indicates error
.c38e	a9 24		        lda #"$"
.c390	9d 0d 01	        sta u0aa0,x         ; prefix addresses with $
.c393	e8		        inx                 ; next position in buffer
.c394	a0 04		        ldy #4              ; non-zero page addresses are 4 hex digits
.c396	ad 0a 01	        lda numbit          ; check numeric base in which address was given
.c399	c9 08		        cmp #8              ; for addresses given in octal or binary
.c39b	90 05		        bcc aaddr           ;   use only the high byte to determine page
.c39d	cc 08 01	        cpy digcnt          ; for decimal or hex, force non-zero page addressing
.c3a0	f0 06		        beq afill0          ;   if address was given with four digits or more
.c3a2	a5 c2		aaddr   lda tmp0+1          ; check whether high byte of address is zero
.c3a4	d0 02		        bne afill0          ; non-zero high byte means we're not in zero page
.c3a6	a0 02		        ldy #2              ; if it's in zero page, addr is 2 hex digits
.c3a8	a9 30		afill0  lda #$30            ; use 0 as placeholder for each hex digit in addr
.c3aa	9d 0d 01	afil0l  sta u0aa0,x         ; put placeholder in assembly buffer
.c3ad	e8		        inx                 ; move to next byte in buffer
.c3ae	88		        dey                 ; decrement number of remaining digits
.c3af	d0 f9		        bne afil0l          ; loop until all digits have been placed
.c3b1	ce 31 02	aform0  dec chrpnt          ; non-numeric input; back 1 char to see what it was
.c3b4	20 1e c7	aform1  jsr getchr          ; get next character
.c3b7	f0 0e		        beq aescan          ; if there is none, we're finished scanning
.c3b9	c9 20		        cmp #$20            ; skip spaces
.c3bb	f0 c5		        beq ascan
.c3bd	9d 0d 01	        sta u0aa0,x         ; store character in assembly buffer
.c3c0	e8		        inx                 ; move to next byte in buffer
.c3c1	e0 0a		        cpx #u0aae-u0aa0    ; is instruction buffer full?
.c3c3	90 bd		        bcc ascan           ; if not, keep scanning
.c3c5	b0 b6		        bcs aerror          ; error if buffer is full
.c3c7	8e 2f 02	aescan  stx store           ; save number of bytes in assembly buffer
.c3ca	a2 00		        ldx #0              ; start at opcode $00 and check every one until
.c3cc	8e 06 01	        stx opcode          ;   we find one that matches our criteria
.c3cf	a2 00		atryop  ldx #0
.c3d1	8e 33 02	        stx u9f             ; reset index into work buffer
.c3d4	ad 06 01	        lda opcode
.c3d7	20 8e c5	        jsr instxx          ; look up instruction format for current opcode
.c3da	ae 00 01	        ldx acmd            ; save addressing command for later
.c3dd	8e 30 02	        stx store+1
.c3e0	aa		        tax                 ; use current opcode as index
.c3e1	bd 63 ca	        lda mnemr,x         ; check right byte of compressed mnemonic
.c3e4	20 b5 c4	        jsr chekop
.c3e7	bd 23 ca	        lda mneml,x         ; check left byte of compressed mnemonic
.c3ea	20 b5 c4	        jsr chekop
.c3ed	a2 06		        ldx #6              ; 6 possible characters to check against operand
.c3ef	e0 03		tryit   cpx #3              ; are we on character 3?
.c3f1	d0 14		        bne trymod          ; if not, check operand characters
.c3f3	ac 01 01	        ldy length          ; otherwise, check number of bytes in operand
.c3f6	f0 0f		        beq trymod          ; if zero, check operand characters
.c3f8	ad 00 01	tryad   lda acmd            ; otherwise, look for an address
.c3fb	c9 e8		        cmp #$e8            ; special case for relative addressing mode
.c3fd	a9 30		        lda #$30            ; '0' is the digit placeholder we're looking for
.c3ff	b0 1e		        bcs try4b           ; acmd >= $e8 indicates relative addressing
.c401	20 b2 c4	        jsr chek2b          ; acmd < $e8 indicates normal addressing
.c404	88		        dey                 ; consume byte
.c405	d0 f1		        bne tryad           ; check for 2 more digits if not zero-page
.c407	0e 00 01	trymod  asl acmd            ; shift a bit out of the addressing command
.c40a	90 0e		        bcc ub4df           ; if it's zero, skip checking current character
.c40c	bd 16 ca	        lda char1-1,x
.c40f	20 b5 c4	        jsr chekop          ; otherwise first character against operand
.c412	bd 1c ca	        lda char2-1,x       ; get second character to check
.c415	f0 03		        beq ub4df           ; if it's zero, skip checking it
.c417	20 b5 c4	        jsr chekop          ; otherwise check it against hte operand
.c41a	ca		ub4df   dex                 ; move to next character
.c41b	d0 d2		        bne tryit           ; repeat tests
.c41d	f0 06		        beq trybran
.c41f	20 b2 c4	try4b   jsr chek2b          ; check for 4 digit address placeholder
.c422	20 b2 c4	        jsr chek2b          ;   by checking for 2 digits twice
.c425	ad 2f 02	trybran lda store           ; get number of bytes in assembly buffer
.c428	cd 33 02	        cmp u9f             ; more bytes left to check?
.c42b	f0 03		        beq abran           ; if not, we've found a match; build instruction
.c42d	4c c2 c4	        jmp bumpop          ; if so, this opcode doesn't match; try the next
.c430	ac 01 01	abran   ldy length          ; get number of bytes in operand
.c433	f0 32		        beq a1byte          ; if none, just output the opcode
.c435	ad 30 02	        lda store+1         ; otherwise check the address format
.c438	c9 9d		        cmp #$9d            ; is it a relative branch?
.c43a	d0 23		        bne objput          ; if not, skip relative branch calculation
.c43c	a5 c1		        lda tmp0            ; calculate the difference between the current
.c43e	e5 c3		        sbc tmp2            ;   address and the branch target (low byte)
.c440	aa		        tax                 ; save it in x
.c441	a5 c2		        lda tmp0+1          ; borrow from the high byte if necessary
.c443	e5 c4		        sbc tmp2+1
.c445	90 08		        bcc abback          ; if result is negative, we're branching back
.c447	d0 66		        bne serror          ; high bytes must be equal when branching forward
.c449	e0 82		        cpx #$82            ; difference between low bytes must be < 130
.c44b	b0 62		        bcs serror          ; error if the address is too far away
.c44d	90 08		        bcc abranx
.c44f	a8		abback  tay                 ; when branching backward high byte of target must
.c450	c8		        iny                 ;   be 1 less than high byte of current address
.c451	d0 5c		        bne serror          ; if not, it's too far away
.c453	e0 82		        cpx #$82            ; difference between low bytes must be < 130
.c455	90 58		        bcc serror          ; if not, it's too far away
.c457	ca		abranx  dex                 ; adjust branch target relative to the
.c458	ca		        dex                 ;   instruction following this one
.c459	8a		        txa
.c45a	ac 01 01	        ldy length          ; load length of operand
.c45d	d0 03		        bne objp2           ; don't use the absolute address
.c45f	b9 c0 00	objput  lda tmp0-1,y        ; get the operand
.c462	91 c3		objp2   sta (tmp2),y        ; store it after the opcode
.c464	88		        dey
.c465	d0 f8		        bne objput          ; copy the other byte of operand if there is one
.c467	ad 06 01	a1byte  lda opcode          ; put opcode into instruction
.c46a	91 c3		        sta (tmp2),y
.c46c	20 dd c6	        jsr crlf            ; carriage return
.c46f	a9 91		        lda #$91            ; back up one line
.c471	20 d2 ff	        jsr chrout
.c474	a0 2e		        ldy #msg7-msgbas    ; "a " prefix
.c476	20 71 c9	        jsr sndclr          ; clear line
.c479	20 10 c5	        jsr dislin          ; disassemble the instruction we just assembled
.c47c	ee 01 01	        inc length          ; instruction length = operand length + 1 byte
.c47f	ad 01 01	        lda length          ;   for the opcode
.c482	20 77 c7	        jsr bumpad2         ; increment address by length of instruction
.c485	a9 41		        lda #"a"            ; stuff keyboard buffer with next assemble command:
.c487	8d 77 02	        sta keyd            ;   "a xxxx " where xxxx is the next address
.c48a	a9 20		        lda #" "            ;   after the previously assembled instruction
.c48c	8d 78 02	        sta keyd+1
.c48f	8d 7d 02	        sta keyd+6
.c492	a5 c4		        lda tmp2+1          ; convert high byte of next address to hex
.c494	20 06 c7	        jsr asctwo
.c497	8d 79 02	        sta keyd+2          ; put it in the keyboard buffer
.c49a	8e 7a 02	        stx keyd+3
.c49d	a5 c3		        lda tmp2            ; convert low byte of next address to hex
.c49f	20 06 c7	        jsr asctwo
.c4a2	8d 7b 02	        sta keyd+4          ; put it in the keyboard buffer
.c4a5	8e 7c 02	        stx keyd+5
.c4a8	a9 07		        lda #7              ; set number of chars in keyboard buffer
.c4aa	85 c6		        sta ndx
.c4ac	4c 61 c0	        jmp strt            ; back to main loop
.c4af	4c 90 c0	serror  jmp error           ; handle error
.c4b2	20 b5 c4	chek2b  jsr chekop          ; check two bytes against value in accumulator
.c4b5	8e 05 01	chekop  stx savx            ; stash x
.c4b8	ae 33 02	        ldx u9f             ; get current index into work buffer
.c4bb	dd 0d 01	        cmp u0aa0,x         ; check whether this opcode matches the buffer
.c4be	f0 0a		        beq opok            ;   matching so far, check the next criteria
.c4c0	68		        pla                 ; didn't match, so throw away return address
.c4c1	68		        pla                 ;   on the stack because we're starting over
.c4c2	ee 06 01	bumpop  inc opcode          ; check the next opcode
.c4c5	f0 e8		        beq serror          ; error if we tried every opcode and none fit
.c4c7	4c cf c3	        jmp atryop          ; start over with new opcode
.c4ca	ee 33 02	opok    inc u9f             ; opcode matches so far; check the next criteria
.c4cd	ae 05 01	        ldx savx            ; restore x
.c4d0	60		        rts
.c4d1	b0 08		disass  bcs dis0ad          ; if no address was given, start from last address
.c4d3	20 38 c7	        jsr copy12          ; copy start address to tmp2
.c4d6	20 ff c5	        jsr getpar          ; get end address in tmp0
.c4d9	90 06		        bcc dis2ad          ; if one was given, skip default
.c4db	a9 14		dis0ad  lda #$14            ; disassemble 14 bytes by default
.c4dd	85 c1		        sta tmp0            ; store length in tmp0
.c4df	d0 05		        bne disgo           ; skip length calculation
.c4e1	20 41 c7	dis2ad  jsr sub12           ; calculate number of bytes between start and end
.c4e4	90 1f		        bcc derror          ; error if end address is before start address
.c4e6	20 6b c9	disgo   jsr cline           ; clear the current line
.c4e9	20 e1 ff	        jsr stop            ; check for stop key
.c4ec	f0 14		        beq disexit         ; exit early if pressed
.c4ee	20 08 c5	        jsr dsout1          ; output disassembly prefix ". "
.c4f1	ee 01 01	        inc length
.c4f4	ad 01 01	        lda length          ; add length of last instruction to start address
.c4f7	20 77 c7	        jsr bumpad2
.c4fa	ad 01 01	        lda length          ; subtract length of last inst from end address
.c4fd	20 51 c7	        jsr suba2
.c500	b0 e4		        bcs disgo
.c502	4c 61 c0	disexit jmp strt            ; back to mainloop
.c505	4c 90 c0	derror  jmp error
.c508	a9 2e		dsout1  lda #"."            ; output ". " prefix to allow edit and reassemble
.c50a	20 d2 ff	        jsr chrout
.c50d	20 d5 c6	        jsr space
.c510	20 c8 c6	dislin  jsr showad          ; show the address of the instruction
.c513	20 d5 c6	        jsr space           ; insert a space
.c516	a0 00		        ldy #0              ; no offset
.c518	b1 c3		        lda (tmp2),y        ; load operand of current instruction
.c51a	20 8e c5	        jsr instxx          ; get mnemonic and addressing mode for opcode
.c51d	48		        pha                 ; save index into mnemonic table
.c51e	ae 01 01	        ldx length          ; get length of operand
.c521	e8		        inx                 ; add 1 byte for opcode
.c522	ca		dsbyt   dex                 ; decrement index
.c523	10 0e		        bpl dshex           ; show hex for byte being disassembled
.c525	8c 32 02	        sty savy            ; save index
.c528	a0 30		        ldy #msg8-msgbas    ; skip 3 spaces
.c52a	20 84 c9	        jsr sndmsg
.c52d	ac 32 02	        ldy savy            ; restore index
.c530	4c 38 c5	        jmp nxbyt
.c533	b1 c3		dshex   lda (tmp2),y        ; show hex for byte
.c535	20 d2 c6	        jsr wrbyte
.c538	c8		nxbyt   iny                 ; next byte
.c539	c0 03		        cpy #3              ; have we output 3 bytes yet?
.c53b	90 e5		        bcc dsbyt           ; if not, loop
.c53d	68		        pla                 ; restore index into mnemonic table
.c53e	a2 03		        ldx #3              ; 3 letters in mnemonic
.c540	20 d6 c5	        jsr propxx          ; print mnemonic
.c543	a2 06		        ldx #6              ; 6 possible address mode character combos
.c545	e0 03		pradr1  cpx #3              ; have we checked the third combo yet?
.c547	d0 16		        bne pradr3          ; if so, output the leading characters
.c549	ac 01 01	        ldy length          ; get the length of the operand
.c54c	f0 11		        beq pradr3          ; if it's zero, there's no operand to print
.c54e	ad 00 01	pradr2  lda acmd            ; otherwise, get the addressing mode
.c551	c9 e8		        cmp #$e8            ; check for relative addressing
.c553	08		        php                 ; save result of check
.c554	b1 c3		        lda (tmp2),y        ; get the operand
.c556	28		        plp                 ; restore result of check
.c557	b0 1d		        bcs relad           ; handle a relative address
.c559	20 f6 c6	        jsr wrtwo           ; output digits from address
.c55c	88		        dey
.c55d	d0 ef		        bne pradr2          ; repeat for next byte of operand, if there is one
.c55f	0e 00 01	pradr3  asl acmd            ; check whether addr mode uses the current char
.c562	90 0e		        bcc pradr4          ; if not, skip it
.c564	bd 16 ca	        lda char1-1,x       ; look up the first char in the table
.c567	20 d2 ff	        jsr chrout          ; print first char
.c56a	bd 1c ca	        lda char2-1,x       ; look up the second char in the table
.c56d	f0 03		        beq pradr4          ; if there's no second character, skip it
.c56f	20 d2 ff	        jsr chrout          ; print second char
.c572	ca		pradr4  dex                 ; next potential address mode character
.c573	d0 d0		        bne pradr1          ; loop if we haven't checked them all yet
.c575	60		        rts                 ; back to caller
.c576	20 82 c5	relad   jsr ub64d           ; calculate absolute address from relative
.c579	18		        clc
.c57a	69 01		        adc #1              ; adjust address relative to next instruction
.c57c	d0 01		        bne relend          ; don't increment high byte unless we overflowed
.c57e	e8		        inx                 ; increment high byte
.c57f	4c cc c6	relend  jmp wraddr          ; print address
.c582	a6 c4		ub64d   ldx tmp2+1          ; get high byte of current address
.c584	a8		        tay                 ; is relative address positive or negative?
.c585	10 01		        bpl relc2           ; if positive, leave high byte alone
.c587	ca		        dex                 ; if negative, decrement high byte
.c588	65 c3		relc2   adc tmp2            ; add relative address to low byte
.c58a	90 01		        bcc relc3           ; if there's no carry, we're done
.c58c	e8		        inx                 ; if there's a carry, increment the high byte
.c58d	60		relc3   rts
.c58e	a8		instxx  tay                 ; stash opcode in accumulator in y for later
.c58f	4a		        lsr a               ; is opcode even or odd?
.c590	90 0b		        bcc ieven
.c592	4a		        lsr a
.c593	b0 17		        bcs err             ; invalid opcodes xxxxxx11
.c595	c9 22		        cmp #$22
.c597	f0 13		        beq err             ; invalid opcode 10001001
.c599	29 07		        and #$07            ; mask bits to 10000xxx
.c59b	09 80		        ora #$80
.c59d	4a		ieven   lsr a               ; lsb determines whether to use left/right nybble
.c59e	aa		        tax                 ; get format index using remaining high bytes
.c59f	bd c5 c9	        lda mode,x
.c5a2	b0 04		        bcs rtmode          ; look at left or right nybble based on carry bit
.c5a4	4a		        lsr a               ; if carry = 0, use left nybble
.c5a5	4a		        lsr a
.c5a6	4a		        lsr a
.c5a7	4a		        lsr a
.c5a8	29 0f		rtmode  and #$0f            ; if carry = 1, use right nybble
.c5aa	d0 04		        bne getfmt
.c5ac	a0 80		err     ldy #$80            ; substitute 10000000 for invalid opcodes
.c5ae	a9 00		        lda #0
.c5b0	aa		getfmt  tax
.c5b1	bd 09 ca	        lda mode2,x         ; lookup operand format using selected nybble
.c5b4	8d 00 01	        sta acmd            ; save for later use
.c5b7	29 03		        and #$03            ; lower 2 bits indicate number of bytes in operand
.c5b9	8d 01 01	        sta length
.c5bc	98		        tya                 ; restore original opcode
.c5bd	29 8f		        and #$8f            ; mask bits to x000xxxx
.c5bf	aa		        tax                 ; save it
.c5c0	98		        tya                 ; restore original opcode
.c5c1	a0 03		        ldy #3
.c5c3	e0 8a		        cpx #$8a            ; check if opcode = 1xxx1010
.c5c5	f0 0b		        beq gtfm4
.c5c7	4a		gtfm2   lsr a               ; transform opcode into index for mnemonic table
.c5c8	90 08		        bcc gtfm4
.c5ca	4a		        lsr a               ; opcodes transformed as follows:
.c5cb	4a		gtfm3   lsr a               ; 1xxx1010->00101xxx
.c5cc	09 20		        ora #$20            ; xxxyyy01->00111xxx
.c5ce	88		        dey                 ; xxxyyy10->00111xxx
.c5cf	d0 fa		        bne gtfm3           ; xxxyy100->00110xxx
.c5d1	c8		        iny                 ; xxxxx000->000xxxxx
.c5d2	88		gtfm4   dey
.c5d3	d0 f2		        bne gtfm2
.c5d5	60		        rts
.c5d6	a8		propxx  tay                 ; use index in accumulator to look up mnemonic
.c5d7	b9 23 ca	        lda mneml,y         ;   and place a temporary copy in store
.c5da	8d 2f 02	        sta store
.c5dd	b9 63 ca	        lda mnemr,y
.c5e0	8d 30 02	        sta store+1
.c5e3	a9 00		prmn1   lda #0              ; clear accumulator
.c5e5	a0 05		        ldy #$05            ; shift 5 times
.c5e7	0e 30 02	prmn2   asl store+1         ; shift right byte
.c5ea	2e 2f 02	        rol store           ; rotate bits from right byte into left byte
.c5ed	2a		        rol a               ; rotate bits from left byte into accumulator
.c5ee	88		        dey                 ; next bit
.c5ef	d0 f6		        bne prmn2           ; loop until all bits shifted
.c5f1	69 3f		        adc #$3f            ; calculate ascii code for letter by adding to '?'
.c5f3	20 d2 ff	        jsr chrout          ; output letter
.c5f6	ca		        dex                 ; next letter
.c5f7	d0 ea		        bne prmn1           ; loop until all 3 letters are output
.c5f9	4c d5 c6	        jmp space           ; output space
.c5fc	ce 31 02	rdpar   dec chrpnt          ; back up one char
.c5ff	20 27 c6	getpar  jsr rdval           ; read the value
.c602	b0 17		        bcs gterr           ; carry set indicates error
.c604	20 1b c7	        jsr gotchr          ; check previous character
.c607	d0 0a		        bne ckterm          ; if it's not null, check if it's a valid separator
.c609	ce 31 02	        dec chrpnt          ; back up one char
.c60c	ad 08 01	        lda digcnt          ; get number of digits read
.c60f	d0 11		        bne getgot          ; found some digits
.c611	f0 0d		        beq gtnil           ; didn't find any digits
.c613	c9 20		ckterm  cmp #$20            ; space or comma are valid separators
.c615	f0 0b		        beq getgot          ; anything else is an error
.c617	c9 2c		        cmp #","
.c619	f0 07		        beq getgot
.c61b	68		gterr   pla                 ; encountered error
.c61c	68		        pla                 ; get rid of command vector pushed on stack
.c61d	4c 90 c0	        jmp error           ; handle error
.c620	38		gtnil   sec                 ; set carry to indicate no parameter found
>c621	24		        .byte $24           ; bit zp opcode consumes next byte (clc)
.c622	18		getgot  clc                 ; clear carry to indicate paremeter returned
.c623	ad 08 01	        lda digcnt          ; return number of digits in a
.c626	60		        rts                 ; return to address pushed from vector table
.c627	a9 00		rdval   lda #0              ; clear temp
.c629	85 c1		        sta tmp0
.c62b	85 c2		        sta tmp0+1
.c62d	8d 08 01	        sta digcnt          ; clear digit counter
.c630	8a		        txa                 ; save x and y
.c631	48		        pha
.c632	98		        tya
.c633	48		        pha
.c634	20 1e c7	rdvmor  jsr getchr          ; get next character from input buffer
.c637	f0 1e		        beq rdnilk          ; null at end of buffer
.c639	c9 20		        cmp #$20            ; skip spaces
.c63b	f0 f7		        beq rdvmor
.c63d	a2 03		        ldx #3              ; check numeric base [$+&%]
.c63f	dd b6 ca	gnmode  cmp hikey,x
.c642	f0 07		        beq gotmod          ; got a match, set up base
.c644	ca		        dex
.c645	10 f8		        bpl gnmode          ; check next base
.c647	e8		        inx                 ; default to hex
.c648	ce 31 02	        dec chrpnt          ; back up one character
.c64b	bc db ca	gotmod  ldy modtab,x        ; get base value
.c64e	bd df ca	        lda lentab,x        ; get bits per digit
.c651	8d 0a 01	        sta numbit          ; store bits per digit
.c654	20 1e c7	nudig   jsr getchr          ; get next char in a
.c657	f0 63		rdnilk  beq rdnil           ; end of number if no more characters
.c659	38		        sec
.c65a	e9 30		        sbc #$30            ; subtract ascii value of 0 to get numeric value
.c65c	90 5e		        bcc rdnil           ; end of number if character was less than 0
.c65e	c9 0a		        cmp #$0a
.c660	90 06		        bcc digmor          ; not a hex digit if less than a
.c662	e9 07		        sbc #$07            ; 7 chars between ascii 9 and a, so subtract 7
.c664	c9 10		        cmp #$10            ; end of number if char is greater than f
.c666	b0 54		        bcs rdnil
.c668	8d 09 01	digmor  sta indig           ; store the digit
.c66b	cc 09 01	        cpy indig           ; compare base with the digit
.c66e	90 4a		        bcc rderr           ; error if the digit >= the base
.c670	f0 48		        beq rderr
.c672	ee 08 01	        inc digcnt          ; increment the number of digits
.c675	c0 0a		        cpy #10
.c677	d0 0a		        bne nodecm          ; skip the next part if not using base 10
.c679	a2 01		        ldx #1
.c67b	b5 c1		declp1  lda tmp0,x          ; stash the previous 16-bit value for later use
.c67d	9d 0b 01	        sta stash,x
.c680	ca		        dex
.c681	10 f8		        bpl declp1
.c683	ae 0a 01	nodecm  ldx numbit          ; number of bits to shift
.c686	06 c1		times2  asl tmp0            ; shift 16-bit value by specified number of bits
.c688	26 c2		        rol tmp0+1
.c68a	b0 2e		        bcs rderr           ; error if we overflowed 16 bits
.c68c	ca		        dex
.c68d	d0 f7		        bne times2          ; shift remaining bits
.c68f	c0 0a		        cpy #10
.c691	d0 18		        bne nodec2          ; skip the next part if not using base 10
.c693	0e 0b 01	        asl stash           ; shift the previous 16-bit value one bit left
.c696	2e 0c 01	        rol stash+1
.c699	b0 1f		        bcs rderr           ; error if we overflowed 16 bits
.c69b	ad 0b 01	        lda stash           ; add shifted previous value to current value
.c69e	65 c1		        adc tmp0
.c6a0	85 c1		        sta tmp0
.c6a2	ad 0c 01	        lda stash+1
.c6a5	65 c2		        adc tmp0+1
.c6a7	85 c2		        sta tmp0+1
.c6a9	b0 0f		        bcs rderr           ; error if we overflowed 16 bits
.c6ab	18		nodec2  clc
.c6ac	ad 09 01	        lda indig           ; load current digit
.c6af	65 c1		        adc tmp0            ; add current digit to low byte
.c6b1	85 c1		        sta tmp0            ; and store result back in low byte
.c6b3	8a		        txa                 ; a=0
.c6b4	65 c2		        adc tmp0+1          ; add carry to high byte
.c6b6	85 c2		        sta tmp0+1          ; and store result back in high byte
.c6b8	90 9a		        bcc nudig           ; get next digit if we didn't overflow
.c6ba	38		rderr   sec                 ; set carry to indicate error
>c6bb	24		        .byte $24           ; bit zp opcode consumes next byte (clc)
.c6bc	18		rdnil   clc                 ; clear carry to indicate success
.c6bd	8c 0a 01	        sty numbit          ; save base of number
.c6c0	68		        pla                 ; restore x and y
.c6c1	a8		        tay
.c6c2	68		        pla
.c6c3	aa		        tax
.c6c4	ad 08 01	        lda digcnt          ; return number of digits in a
.c6c7	60		        rts
.c6c8	a5 c3		showad  lda tmp2
.c6ca	a6 c4		        ldx tmp2+1
.c6cc	48		wraddr  pha                 ; save low byte
.c6cd	8a		        txa                 ; put high byte in a
.c6ce	20 f6 c6	        jsr wrtwo           ; output high byte
.c6d1	68		        pla                 ; restore low byte
.c6d2	20 f6 c6	wrbyte  jsr wrtwo           ; output byte in a
.c6d5	a9 20		space   lda #$20            ; output space
.c6d7	d0 0f		        bne flip
.c6d9	c9 0d		chout   cmp #$0d            ; output char with special handling of cr
.c6db	d0 0b		        bne flip
.c6dd	a9 0d		crlf    lda #$0d            ; load cr in a
.c6df	24 13		        bit $13             ; check default channel
.c6e1	10 05		        bpl flip            ; if high bit is clear output cr only
.c6e3	20 d2 ff	        jsr chrout          ; otherwise output cr+lf
.c6e6	a9 0a		        lda #$0a            ; output lf
.c6e8	4c d2 ff	flip    jmp chrout
.c6eb	20 dd c6	fresh   jsr crlf            ; output cr
.c6ee	a9 20		        lda #$20            ; load space in a
.c6f0	20 d2 ff	        jsr chrout
.c6f3	4c 74 c9	        jmp snclr
.c6f6	8e 05 01	wrtwo   stx savx            ; save x
.c6f9	20 06 c7	        jsr asctwo          ; get hex chars for byte in x (lower) and a (upper)
.c6fc	20 d2 ff	        jsr chrout          ; output upper nybble
.c6ff	8a		        txa                 ; transfer lower to a
.c700	ae 05 01	        ldx savx            ; restore x
.c703	4c d2 ff	        jmp chrout          ; output lower nybble
.c706	48		asctwo  pha                 ; save byte
.c707	20 10 c7	        jsr ascii           ; do low nybble
.c70a	aa		        tax                 ; save in x
.c70b	68		        pla                 ; restore byte
.c70c	4a		        lsr a               ; shift upper nybble down
.c70d	4a		        lsr a
.c70e	4a		        lsr a
.c70f	4a		        lsr a
.c710	29 0f		ascii   and #$0f            ; clear upper nibble
.c712	c9 0a		        cmp #$0a            ; if less than a, skip next step
.c714	90 02		        bcc asc1
.c716	69 06		        adc #6              ; skip ascii chars between 9 and a
.c718	69 30		asc1    adc #$30            ; add ascii char 0 to value
.c71a	60		        rts
.c71b	ce 31 02	gotchr  dec chrpnt
.c71e	8e 05 01	getchr  stx savx
.c721	ae 31 02	        ldx chrpnt          ; get pointer to next char
.c724	bd 00 02	        lda inbuff,x        ; load next char in a
.c727	f0 06		        beq nochar          ; null, :, or ? signal end of buffer
.c729	c9 3a		        cmp #":"
.c72b	f0 02		        beq nochar
.c72d	c9 3f		        cmp #"?"
.c72f	08		nochar  php
.c730	ee 31 02	        inc chrpnt          ; next char
.c733	ae 05 01	        ldx savx
.c736	28		        plp                 ; z flag will signal last character
.c737	60		        rts
.c738	a5 c1		copy12  lda tmp0            ; low byte
.c73a	85 c3		        sta tmp2
.c73c	a5 c2		        lda tmp0+1          ; high byte
.c73e	85 c4		        sta tmp2+1
.c740	60		        rts
.c741	38		sub12   sec
.c742	a5 c1		        lda tmp0            ; subtract low byte
.c744	e5 c3		        sbc tmp2
.c746	85 c1		        sta tmp0
.c748	a5 c2		        lda tmp0+1
.c74a	e5 c4		        sbc tmp2+1          ; subtract high byte
.c74c	85 c2		        sta tmp0+1
.c74e	60		        rts
.c74f	a9 01		suba1   lda #1              ; shortcut to decrement by 1
.c751	8d 05 01	suba2   sta savx            ; subtrahend in accumulator
.c754	38		        sec
.c755	a5 c1		        lda tmp0            ; minuend in low byte
.c757	ed 05 01	        sbc savx
.c75a	85 c1		        sta tmp0
.c75c	a5 c2		        lda tmp0+1          ; borrow from high byte
.c75e	e9 00		        sbc #0
.c760	85 c2		        sta tmp0+1
.c762	60		        rts
.c763	38		sub13   sec
.c764	ad 2f 02	        lda store
.c767	e9 01		        sbc #1              ; decrement low byte
.c769	8d 2f 02	        sta store
.c76c	ad 30 02	        lda store+1
.c76f	e9 00		        sbc #0              ; borrow from high byte
.c771	8d 30 02	        sta store+1
.c774	60		        rts
.c775	a9 01		adda2   lda #1              ; shortcut to increment by 1
.c777	18		bumpad2 clc
.c778	65 c3		        adc tmp2            ; add value in accumulator to low byte
.c77a	85 c3		        sta tmp2
.c77c	90 02		        bcc bumpex
.c77e	e6 c4		        inc tmp2+1          ; carry to high byte
.c780	60		bumpex  rts
.c781	38		sub21   sec
.c782	a5 c3		        lda tmp2            ; decrement low byte
.c784	e9 01		        sbc #1
.c786	85 c3		        sta tmp2
.c788	a5 c4		        lda tmp2+1          ; borrow from high byte
.c78a	e9 00		        sbc #0
.c78c	85 c4		        sta tmp2+1
.c78e	60		        rts
.c78f	b0 0a		copy1p  bcs cpy1px          ; do nothing if parameter is empty
.c791	a5 c1		        lda tmp0            ; copy low byte
.c793	a4 c2		        ldy tmp0+1          ; copy high byte
.c795	8d 29 02	        sta pcl
.c798	8c 28 02	        sty pch
.c79b	60		cpy1px  rts
.c79c	b0 23		getdif  bcs gdifx           ; exit with error if no parameter given
.c79e	20 38 c7	        jsr copy12          ; save start address in tmp2
.c7a1	20 ff c5	        jsr getpar          ; get end address in tmp0
.c7a4	b0 1b		        bcs gdifx           ; exit with error if no parameter given
.c7a6	a5 c1		        lda tmp0            ; save end address in stash
.c7a8	8d 0b 01	        sta stash
.c7ab	a5 c2		        lda tmp0+1
.c7ad	8d 0c 01	        sta stash+1
.c7b0	20 41 c7	        jsr sub12           ; subtract start address from end address
.c7b3	a5 c1		        lda tmp0
.c7b5	8d 2f 02	        sta store           ; save difference in store
.c7b8	a5 c2		        lda tmp0+1
.c7ba	8d 30 02	        sta store+1
.c7bd	90 02		        bcc gdifx           ; error if start address is after end address
.c7bf	18		        clc                 ; clear carry to indicate success
>c7c0	24		        .byte $24           ; bit zp opcode consumes next byte (sec)
.c7c1	38		gdifx   sec                 ; set carry to indicate error
.c7c2	60		        rts
.c7c3	20 fc c5	convrt  jsr rdpar           ; read a parameter
.c7c6	20 eb c6	        jsr fresh           ; next line and clear
.c7c9	a9 24		        lda #"$"            ; output $ sigil for hex
.c7cb	20 d2 ff	        jsr chrout
.c7ce	a5 c1		        lda tmp0            ; load the 16-bit value entered
.c7d0	a6 c2		        ldx tmp0+1
.c7d2	20 cc c6	        jsr wraddr          ; print it in 4 hex digits
.c7d5	20 eb c6	        jsr fresh
.c7d8	a9 2b		        lda #"+"            ; output + sigil for decimal
.c7da	20 d2 ff	        jsr chrout
.c7dd	20 0e c8	        jsr cvtdec          ; convert to bcd using hardware mode
.c7e0	a9 00		        lda #0              ; clear digit counter
.c7e2	a2 06		        ldx #6              ; max digits + 1
.c7e4	a0 03		        ldy #3              ; bits per digit - 1
.c7e6	20 48 c8	        jsr nmprnt          ; print result without leading zeros
.c7e9	20 eb c6	        jsr fresh           ; next line and clear
.c7ec	a9 26		        lda #"&"            ; print & sigil for octal
.c7ee	20 d2 ff	        jsr chrout
.c7f1	a9 00		        lda #0              ; clear digit counter
.c7f3	a2 08		        ldx #8              ; max digits + 1
.c7f5	a0 02		        ldy #2              ; bits per digit - 1
.c7f7	20 37 c8	        jsr prinum          ; output number
.c7fa	20 eb c6	        jsr fresh           ; next line and clear
.c7fd	a9 25		        lda #"%"            ; print % sigil for binary
.c7ff	20 d2 ff	        jsr chrout
.c802	a9 00		        lda #0              ; clear digit counter
.c804	a2 18		        ldx #$18            ; max digits + 1
.c806	a0 00		        ldy #0              ; bits per digit - 1
.c808	20 37 c8	        jsr prinum          ; output number
.c80b	4c 61 c0	        jmp strt            ; back to mainloop
.c80e	20 38 c7	cvtdec  jsr copy12          ; copy value from tmp0 to tmp2
.c811	a9 00		        lda #0
.c813	a2 02		        ldx #2              ; clear 3 bytes in work buffer
.c815	9d 0d 01	decml1  sta u0aa0,x
.c818	ca		        dex
.c819	10 fa		        bpl decml1
.c81b	a0 10		        ldy #16             ; 16 bits in input
.c81d	08		        php                 ; save status register
.c81e	78		        sei                 ; make sure no interrupts occur with bcd enabled
.c81f	f8		        sed
.c820	06 c3		decml2  asl tmp2            ; rotate bytes out of input low byte
.c822	26 c4		        rol tmp2+1          ; .. into high byte and carry bit
.c824	a2 02		        ldx #2              ; process 3 bytes
.c826	bd 0d 01	decdbl  lda u0aa0,x         ; load current value of byte
.c829	7d 0d 01	        adc u0aa0,x         ; add it to itself plus the carry bit
.c82c	9d 0d 01	        sta u0aa0,x         ; store it back in the same location
.c82f	ca		        dex                 ; decrement byte counter
.c830	10 f4		        bpl decdbl          ; loop until all bytes processed
.c832	88		        dey                 ; decrement bit counter
.c833	d0 eb		        bne decml2          ; loop until all bits processed
.c835	28		        plp                 ; restore processor status
.c836	60		        rts
.c837	48		prinum  pha                 ; save accumulator
.c838	a5 c1		        lda tmp0            ; copy input low byte to work buffer
.c83a	8d 0f 01	        sta u0aa0+2
.c83d	a5 c2		        lda tmp0+1          ; copy input high byte to work buffer
.c83f	8d 0e 01	        sta u0aa0+1
.c842	a9 00		        lda #0              ; clear overflow byte in work buffer
.c844	8d 0d 01	        sta u0aa0
.c847	68		        pla                 ; restore accumulator
.c848	8d 08 01	nmprnt  sta digcnt          ; number of digits in accumulator
.c84b	8c 0a 01	        sty numbit          ; bits per digit passed in y register
.c84e	ac 0a 01	digout  ldy numbit          ; get bits to process
.c851	a9 00		        lda #0              ; clear accumulator
.c853	0e 0f 01	rolbit  asl u0aa0+2         ; shift bits out of low byte
.c856	2e 0e 01	        rol u0aa0+1         ; ... into high byte
.c859	2e 0d 01	        rol u0aa0           ; ... into overflow byte
.c85c	2a		        rol a               ; ... into accumulator
.c85d	88		        dey                 ; decrement bit counter
.c85e	10 f3		        bpl rolbit          ; loop until all bits processed
.c860	a8		        tay                 ; check whether accumulator is 0
.c861	d0 09		        bne nzero           ; if not, print it
.c863	e0 01		        cpx #1              ; have we output the max number of digits?
.c865	f0 05		        beq nzero           ; if not, print it
.c867	ac 08 01	        ldy digcnt          ; how many digits have we output?
.c86a	f0 08		        beq zersup          ; skip output if digit is 0
.c86c	ee 08 01	nzero   inc digcnt          ; increment digit counter
.c86f	09 30		        ora #$30            ; add numeric value to ascii '0' to get ascii char
.c871	20 d2 ff	        jsr chrout          ; output character
.c874	ca		zersup  dex                 ; decrement number of leading zeros
.c875	d0 d7		        bne digout          ; next digit
.c877	60		        rts
.c878	d0 03		dstat   bne chgdev          ; if device address was given, use it
.c87a	a2 08		        ldx #8              ; otherwise, default to 8
>c87c	2c		        .byte $2c           ; absolute bit opcode consumes next word (ldx tmp0)
.c87d	a6 c1		chgdev  ldx tmp0            ; load device address from parameter
.c87f	e0 04		        cpx #4              ; make sure device address is in range 4-31
.c881	90 58		        bcc ioerr
.c883	e0 20		        cpx #32
.c885	b0 54		        bcs ioerr
.c887	86 c1		        stx tmp0
.c889	a9 00		        lda #0              ; clear status
.c88b	85 90		        sta satus
.c88d	85 b7		        sta fnlen           ; empty filename
.c88f	20 1e c7	        jsr getchr          ; get next character
.c892	f0 24		        beq instat1         ; null, display status
.c894	ce 31 02	        dec chrpnt          ; back up 1 char
.c897	c9 24		        cmp #"$"            ; $, display directory
.c899	f0 43		        beq direct
.c89b	a5 c1		        lda tmp0            ; command specified device to listen
.c89d	20 b1 ff	        jsr listen
.c8a0	a9 6f		        lda #$6f            ; secondary address 15 (only low nybble used)
.c8a2	20 93 ff	        jsr second
.c8a5	ae 31 02	dcomd   ldx chrpnt          ; get next character from buffer
.c8a8	ee 31 02	        inc chrpnt
.c8ab	bd 00 02	        lda inbuff,x
.c8ae	f0 05		        beq instat          ; break out of loop if it's null
.c8b0	20 a8 ff	        jsr ciout           ; otherwise output it to the serial bus
.c8b3	90 f0		        bcc dcomd           ; unconditional loop: ciout clears carry before rts
.c8b5	20 ae ff	instat  jsr unlsn           ; command device to unlisten
.c8b8	20 dd c6	instat1 jsr crlf            ; new line
.c8bb	a5 c1		        lda tmp0            ; load device address
.c8bd	20 b4 ff	        jsr talk            ; command device to talk
.c8c0	a9 6f		        lda #$6f            ; secondary address 15 (only low nybble used)
.c8c2	20 96 ff	        jsr tksa
.c8c5	20 a5 ff	rdstat  jsr acptr           ; read byte from serial bus
.c8c8	20 d2 ff	        jsr chrout          ; print it
.c8cb	c9 0d		        cmp #$0d            ; if the byte is cr, exit loop
.c8cd	f0 06		        beq dexit
.c8cf	a5 90		        lda satus           ; check status
.c8d1	29 bf		        and #$bf            ; ignore eoi bit
.c8d3	f0 f0		        beq rdstat          ; if no errors, read next byte
.c8d5	20 ab ff	dexit   jsr untlk           ; command device to stop talking
.c8d8	4c 61 c0	        jmp strt            ; back to mainloop
.c8db	4c 90 c0	ioerr   jmp error           ; handle error
.c8de	a5 c1		direct  lda tmp0            ; load device address
.c8e0	20 b1 ff	        jsr listen          ; command device to listen
.c8e3	a9 f0		        lda #$f0            ; secondary address 0 (only low nybble used)
.c8e5	20 93 ff	        jsr second
.c8e8	ae 31 02	        ldx chrpnt          ; get index of next character
.c8eb	bd 00 02	dir2    lda inbuff,x        ; get next character from buffer
.c8ee	f0 06		        beq dir3            ; break if it's null
.c8f0	20 a8 ff	        jsr ciout           ; send character to device
.c8f3	e8		        inx                 ; increment characer index
.c8f4	d0 f5		        bne dir2            ; loop if it hasn't wrapped to zero
.c8f6	20 ae ff	dir3    jsr unlsn           ; command device to unlisten
.c8f9	20 dd c6	        jsr crlf            ; new line
.c8fc	a5 c1		        lda tmp0            ; load device address
.c8fe	48		        pha                 ; save on stack
.c8ff	20 b4 ff	        jsr talk            ; command device to talk
.c902	a9 60		        lda #$60            ; secondary address 0 (only low nybble used)
.c904	20 96 ff	        jsr tksa
.c907	a0 03		        ldy #3              ; read 3 16-bit values from device
.c909	8c 2f 02	dirlin  sty store           ;   ignore the first 2; 3rd is file size
.c90c	20 a5 ff	dlink   jsr acptr           ; read low byte from device
.c90f	85 c1		        sta tmp0            ; store it
.c911	a5 90		        lda satus           ; check status
.c913	d0 44		        bne drexit          ; exit if error or eof occurred
.c915	20 a5 ff	        jsr acptr           ; read high byte from device
.c918	85 c2		        sta tmp0+1          ; store it
.c91a	a5 90		        lda satus           ; check status
.c91c	d0 3b		        bne drexit          ; exit if error or eof cocurred
.c91e	ce 2f 02	        dec store           ; decrement byte count
.c921	d0 e9		        bne dlink           ; loop if bytes remain
.c923	20 0e c8	        jsr cvtdec          ; convert last 16-bit value to decimal
.c926	a9 00		        lda #0              ; clear digit count
.c928	a2 06		        ldx #6              ; max 6 digits
.c92a	a0 03		        ldy #3              ; 3 bits per digit
.c92c	20 48 c8	        jsr nmprnt          ; output number
.c92f	a9 20		        lda #" "            ; output space
.c931	20 d2 ff	        jsr chrout
.c934	20 a5 ff	dname   jsr acptr           ; get a filename character from the device
.c937	f0 0a		        beq dmore           ; if it's null, break out of loop
.c939	a6 90		        ldx satus           ; check for errors or eof
.c93b	d0 1c		        bne drexit          ; if found exit early
.c93d	20 d2 ff	        jsr chrout          ; output character
.c940	18		        clc
.c941	90 f1		        bcc dname           ; unconditional branch to read next char
.c943	20 dd c6	dmore   jsr crlf
.c946	20 e1 ff	        jsr stop            ; check for stop key
.c949	f0 0e		        beq drexit          ; exit early if pressed
.c94b	20 e4 ff	        jsr getin           ; pause if a key was pressed
.c94e	f0 05		        beq nopaws
.c950	20 e4 ff	paws    jsr getin           ; wait until another key is pressed
.c953	f0 fb		        beq paws
.c955	a0 02		nopaws  ldy #2
.c957	d0 b0		        bne dirlin          ; unconditional branch to read next file
.c959	20 ab ff	drexit  jsr untlk           ; command device to untalk
.c95c	68		        pla                 ; restore accumulator
.c95d	20 b1 ff	        jsr listen          ; command device to listen
.c960	a9 e0		        lda #$e0            ; secondary address 0 (only low nybble is used)
.c962	20 93 ff	        jsr second
.c965	20 ae ff	        jsr unlsn           ; command device to unlisten
.c968	4c 61 c0	        jmp strt            ; back to mainloop
.c96b	20 dd c6	cline   jsr crlf            ; send cr+lf
.c96e	4c 74 c9	        jmp snclr           ; clear line
.c971	20 84 c9	sndclr  jsr sndmsg
.c974	a0 28		snclr   ldy #$28            ; loop 40 times
.c976	a9 20		snclp   lda #$20            ; output space character
.c978	20 d2 ff	        jsr chrout
.c97b	a9 14		        lda #$14            ; output delete character
.c97d	20 d2 ff	        jsr chrout
.c980	88		        dey
.c981	d0 f3		        bne snclp
.c983	60		        rts
.c984	b9 92 c9	sndmsg  lda msgbas,y        ; y contains offset in msg table
.c987	08		        php
.c988	29 7f		        and #$7f            ; strip high bit before output
.c98a	20 d9 c6	        jsr chout
.c98d	c8		        iny
.c98e	28		        plp
.c98f	10 f3		        bpl sndmsg          ; loop until high bit is set
.c991	60		        rts
=$c992			msgbas  =*
>c992	0d		msg2    .byte $0d               ; header for registers
>c993	20 20 20 50	        .text "   pc  sr ac xr yr sp   v1.2"
>c997	43 20 20 53 52 20 41 43 20 58 52 20 59 52 20 53
>c9a7	50 20 20 20 56 31 2e 32
>c9af	8d		        .byte $0d+$80
>c9b0	1d bf		msg3    .byte $1d,$3f+$80       ; syntax error: move right, display "?"
>c9b2	2e 2e 53 59	msg4    .text "..sys"           ; sys call to enter monitor
>c9b6	53
>c9b7	a0		        .byte $20+$80
>c9b8	3a 92		msg5    .byte $3a,$12+$80       ; ":" then rvs on for memory ascii dump
>c9ba	20 45 52 52	msg6    .text " erro"           ; i/o error: display " error"
>c9be	4f
>c9bf	d2		        .byte "r"+$80
>c9c0	41 a0		msg7    .byte $41,$20+$80       ; assemble next instruction: "a " + addr
>c9c2	20 20		msg8    .text "  "              ; pad non-existent byte: skip 3 spaces
>c9c4	a0		        .byte $20+$80
>c9c5	40 02 45 03	mode    .byte $40,$02,$45,$03   ; even opcodes
>c9c9	d0 08 40 09	        .byte $d0,$08,$40,$09
>c9cd	30 22 45 33	        .byte $30,$22,$45,$33
>c9d1	d0 08 40 09	        .byte $d0,$08,$40,$09
>c9d5	40 02 45 33	        .byte $40,$02,$45,$33
>c9d9	d0 08 40 09	        .byte $d0,$08,$40,$09
>c9dd	40 02 45 b3	        .byte $40,$02,$45,$b3
>c9e1	d0 08 40 09	        .byte $d0,$08,$40,$09
>c9e5	00 22 44 33	        .byte $00,$22,$44,$33
>c9e9	d0 8c 44 00	        .byte $d0,$8c,$44,$00
>c9ed	11 22 44 33	        .byte $11,$22,$44,$33
>c9f1	d0 8c 44 9a	        .byte $d0,$8c,$44,$9a
>c9f5	10 22 44 33	        .byte $10,$22,$44,$33
>c9f9	d0 08 40 09	        .byte $d0,$08,$40,$09
>c9fd	10 22 44 33	        .byte $10,$22,$44,$33
>ca01	d0 08 40 09	        .byte $d0,$08,$40,$09
>ca05	62 13 78 a9	        .byte $62,$13,$78,$a9   ; opcodes ending in 01
>ca09	00		mode2   .byte $00   ; 000 000    00                  0   error
>ca0a	21		        .byte $21   ; 001 000    01      #$00        1   immediate
>ca0b	81		        .byte $81   ; 100 000    01      $00         2   zero-page
>ca0c	82		        .byte $82   ; 100 000    10      $0000       3   absolute
>ca0d	00		        .byte $00   ; 000 000    00                  4   implied
>ca0e	00		        .byte $00   ; 000 000    00                  5   accumulator
>ca0f	59		        .byte $59   ; 010 110    01      ($00,x)     6   indirect,x
>ca10	4d		        .byte $4d   ; 010 011    01      ($00),y     7   indirect,y
>ca11	91		        .byte $91   ; 100 100    01      $00,x       8   zero-page,x
>ca12	92		        .byte $92   ; 100 100    10      $0000,x     9   absolute,x
>ca13	86		        .byte $86   ; 100 001    10      $0000,y     a   absolute,y
>ca14	4a		        .byte $4a   ; 010 010    10      ($0000)     b   indirect
>ca15	85		        .byte $85   ; 100 001    01      $00,y       c   zero-page,y
>ca16	9d		        .byte $9d   ; 100 111    01      $0000*      d   relative
>ca17	2c 29 2c	char1   .byte $2c,$29,$2c       ; ","  ")"  ","
>ca1a	23 28 24	        .byte $23,$28,$24       ; "#"  "("  "$"
>ca1d	59 00 58	char2   .byte $59,$00,$58       ; "y"   0   "x"
>ca20	24 24 00	        .byte $24,$24,$00       ; "$"  "$"   0
>ca23	1c 8a 1c 23	mneml   .byte $1c,$8a,$1c,$23   ; brk php bpl clc
>ca27	5d 8b 1b a1	        .byte $5d,$8b,$1b,$a1   ; jsr plp bmi sec
>ca2b	9d 8a 1d 23	        .byte $9d,$8a,$1d,$23   ; rti pha bvc cli
>ca2f	9d 8b 1d a1	        .byte $9d,$8b,$1d,$a1   ; rts pla bvs sei
>ca33	00 29 19 ae	        .byte $00,$29,$19,$ae   ; ??? dey bcc tya
>ca37	69 a8 19 23	        .byte $69,$a8,$19,$23   ; ldy tay bcs clv
>ca3b	24 53 1b 23	        .byte $24,$53,$1b,$23   ; cpy iny bne cld
>ca3f	24 53 19 a1	        .byte $24,$53,$19,$a1   ; cpx inx beq sed
>ca43	00 1a 5b 5b	        .byte $00,$1a,$5b,$5b   ; ??? bit jmp jmp
>ca47	a5 69 24 24	        .byte $a5,$69,$24,$24   ; sty ldy cpy cpx
>ca4b	ae ae a8 ad	        .byte $ae,$ae,$a8,$ad   ; txa txs tax tsx
>ca4f	29 00 7c 00	        .byte $29,$00,$7c,$00   ; dex ??? nop ???
>ca53	15 9c 6d 9c	        .byte $15,$9c,$6d,$9c   ; asl rol lsr ror
>ca57	a5 69 29 53	        .byte $a5,$69,$29,$53   ; stx ldx dec inc
>ca5b	84 13 34 11	        .byte $84,$13,$34,$11   ; ora and eor adc
>ca5f	a5 69 23 a0	        .byte $a5,$69,$23,$a0   ; sta lda cmp sbc
>ca63	d8 62 5a 48	mnemr   .byte $d8,$62,$5a,$48   ; brk php bpl clc
>ca67	26 62 94 88	        .byte $26,$62,$94,$88   ; jsr plp bmi sec
>ca6b	54 44 c8 54	        .byte $54,$44,$c8,$54   ; rti pha bvc cli
>ca6f	68 44 e8 94	        .byte $68,$44,$e8,$94   ; rts pla bvs sei
>ca73	00 b4 08 84	        .byte $00,$b4,$08,$84   ; ??? dey bcc tya
>ca77	74 b4 28 6e	        .byte $74,$b4,$28,$6e   ; ldy tay bcs clv
>ca7b	74 f4 cc 4a	        .byte $74,$f4,$cc,$4a   ; cpy iny bne cld
>ca7f	72 f2 a4 8a	        .byte $72,$f2,$a4,$8a   ; cpx inx beq sed
>ca83	00 aa a2 a2	        .byte $00,$aa,$a2,$a2   ; ??? bit jmp jmp
>ca87	74 74 74 72	        .byte $74,$74,$74,$72   ; sty ldy cpy cpx
>ca8b	44 68 b2 32	        .byte $44,$68,$b2,$32   ; txa txs tax tsx
>ca8f	b2 00 22 00	        .byte $b2,$00,$22,$00   ; dex ??? nop ???
>ca93	1a 1a 26 26	        .byte $1a,$1a,$26,$26   ; asl rol lsr ror
>ca97	72 72 88 c8	        .byte $72,$72,$88,$c8   ; stx ldx dec inc
>ca9b	c4 ca 26 48	        .byte $c4,$ca,$26,$48   ; ora and eor adc
>ca9f	44 44 a2 c8	        .byte $44,$44,$a2,$c8   ; sta lda cmp sbc
>caa3	0d 20 20 20	        .byte $0d,$20,$20,$20
>caa7	41 43 44 46	keyw    .text "acdfghjmrtx@.>;"
>caab	47 48 4a 4d 52 54 58 40 2e 3e 3b
>cab6	24 2b 26 25	hikey   .text "$+&%lsv"
>caba	4c 53 56
=$cabd			keytop  =*
>cabd	43 c3 9b c1	kaddr   .word assem-1,compar-1,disass-1,fill-1
>cac1	d0 c4 1e c3
>cac5	26 c1 1f c2	        .word goto-1,hunt-1,jsub-1,dsplym-1
>cac9	44 c1 b9 c0
>cacd	3e c0 9e c1	        .word dsplyr-1,trans-1,exit-1,dstat-1
>cad1	b6 c0 77 c8
>cad5	43 c3 06 c1	        .word assem-1,altm-1,altr-1
>cad9	ef c0
>cadb	10 0a 08 02	modtab  .byte $10,$0a,$08,02    ; modulo number systems
>cadf	04 03 03 01	lentab  .byte $04,$03,$03,$01   ; bits per digit
>cae3	30 c0		linkad  .word break             ; address of brk handler
>cae5	00 c0		supad   .word super             ; address of entry point

;******  End of listing
