
; 64tass Turbo Assembler Macro V1.54.1900 listing file
; 64tass -C -m -a -I /Users/Locals/CBM/00-usbkey32go/d-00-64tass-src/C64/lib -i -L supermon64.txt -o supermon64.prg supermon64.asm
; Sat Jan 31 20:02:56 2026

;Offset	;Hex		;Source

;******  Processing input file: supermon64.asm

=$c1			tmp0    = $c1               ; used to return input, often holds end address
=$c3			tmp2    = $c3               ; usually holds start address
=$90			satus   = $90               ; kernal i/o status word
=$b7			fnlen   = $b7               ; length of current filename
=$b9			sadd    = $b9               ; current secondary address (official name sa)
=$ba			fa      = $ba               ; current device number
=$bb			fnadr   = $bb               ; pointer to current filename
=$c6			ndx     = $c6               ; number of characters in keyboard buffer
=$0277			keyd    = $0277             ; keyboard buffer
=$0316			bkvec   = $0316             ; brk instruction vector (official name cbinv)
>0100			acmd    .fill 1             ; addressing command
>0101			length  .fill 1             ; length of operand
>0102			mnemw   .fill 3             ; 3 letter mnemonic buffer
>0105			savx    .fill 1             ; 1 byte temp storage, often to save x register
>0106			opcode  .fill 1             ; current opcode for assembler/disassembler
>0107			upflg   .fill 1             ; flag: count up (bit 7 clear) or down (bit 7 set)
>0108			digcnt  .fill 1             ; digit count
>0109			indig   .fill 1             ; numeric value of single digit
>010a			numbit  .fill 1             ; numeric base of input
>010b			stash   .fill 2             ; 2-byte temp storage
>010d			u0aa0   .fill 10            ; work buffer
=$0117			u0aae   =*                  ; end of work buffer
>0117			stage   .fill 30            ; staging buffer for filename, search, etc.
=$0135			estage  =*                  ; end of staging buffer
>0200			inbuff  .fill 40            ; 40-character input buffer
=$0228			endin   =*                  ; end of input buffer
>0228			pch     .fill 1             ; program counter high byte
>0229			pcl     .fill 1             ; program counter low byte
>022a			sr      .fill 1             ; status register
>022b			acc     .fill 1             ; accumulator
>022c			xr      .fill 1             ; x register
>022d			yr      .fill 1             ; y register
>022e			sp      .fill 1             ; stack pointer
>022f			store   .fill 2             ; 2-byte temp storage
>0231			chrpnt  .fill 1             ; current position in input buffer
>0232			savy    .fill 1             ; temp storage, often to save y register
>0233			u9f     .fill 1             ; index into assembler work buffer
=$ff90			setmsg  = $ff90             ; set kernel message control flag
=$ff93			second  = $ff93             ; set secondary address after listen
=$ff96			tksa    = $ff96             ; send secondary address after talk
=$ffb1			listen  = $ffb1             ; command serial bus device to listen
=$ffb4			talk    = $ffb4             ; command serial bus device to talk
=$ffba			setlfs  = $ffba             ; set logical file parameters
=$ffbd			setnam  = $ffbd             ; set filename
=$ffa5			acptr   = $ffa5             ; input byte from serial bus
=$ffa8			ciout   = $ffa8             ; output byte to serial bus
=$ffab			untlk   = $ffab             ; command serial bus device to untalk
=$ffae			unlsn   = $ffae             ; command serial bus device to unlisten
=$ffc6			chkin   = $ffc6             ; define input channel
=$ffcc			clrchn  = $ffcc             ; restore default devices
=$ffcf			input   = $ffcf             ; input a character (official name chrin)
=$ffd2			chrout  = $ffd2             ; output a character
=$ffd5			load    = $ffd5             ; load from device
=$ffd8			save    = $ffd8             ; save to device
=$ffe1			stop    = $ffe1             ; check the stop key
=$ffe4			getin   = $ffe4             ; get a character
=$c000			org     = $c000
.c000	a9 93		super   lda #147
.c002	20 d2 ff	        jsr $ffd2
.c005	a0 20		        ldy #msg4-msgbas    ; display "..sys "
.c007	20 89 c9	        jsr sndmsg
.c00a	ad ea ca	        lda supad           ; store entry point address in tmp0
.c00d	85 c1		        sta tmp0
.c00f	ad eb ca	        lda supad+1
.c012	85 c2		        sta tmp0+1
.c014	20 13 c8	        jsr cvtdec          ; convert address to decimal
.c017	a9 00		        lda #0
.c019	a2 06		        ldx #6
.c01b	a0 03		        ldy #3
.c01d	20 4d c8	        jsr nmprnt          ; print entry point address
.c020	20 e2 c6	        jsr crlf
.c023	ad e8 ca	        lda linkad          ; set brk vector
.c026	8d 16 03	        sta bkvec
.c029	ad e9 ca	        lda linkad+1
.c02c	8d 17 03	        sta bkvec+1
.c02f	a9 80		        lda #$80            ; disable kernel control messages
.c031	20 90 ff	        jsr setmsg          ; and enable error messages
.c034	00		        brk
.c035	a2 05		break   ldx #$05            ; pull registers off the stack
.c037	68		bstack  pla                 ; order: y,x,a,sr,pcl,pch
.c038	9d 28 02	        sta pch,x           ; store in memory
.c03b	ca		        dex
.c03c	10 f9		        bpl bstack
.c03e	d8		        cld                 ; disable bcd mode
.c03f	ba		        tsx                 ; store stack pointer in memory
.c040	8e 2e 02	        stx sp
.c043	58		        cli                 ; enable interupts
.c044	a0 00		dsplyr  ldy #msg2-msgbas    ; display headers
.c046	20 76 c9	        jsr sndclr
.c049	a9 3b		        lda #$3b            ; prefix registers with "; " to allow editing
.c04b	20 d2 ff	        jsr chrout
.c04e	a9 20		        lda #$20
.c050	20 d2 ff	        jsr chrout
.c053	ad 28 02	        lda pch             ; print 2-byte program counter
.c056	20 fb c6	        jsr wrtwo
.c059	a0 01		        ldy #1              ; start 1 byte after pc high byte
.c05b	b9 28 02	disj    lda pch,y           ; loop through rest of the registers
.c05e	20 d7 c6	        jsr wrbyte          ; print 1-byte register value
.c061	c8		        iny
.c062	c0 07		        cpy #7              ; there are a total of 5 registers to print
.c064	90 f5		        bcc disj
.c066	20 e2 c6	strt    jsr crlf            ; new line
.c069	a2 00		        ldx #0              ; point at start of input buffer
.c06b	8e 31 02	        stx chrpnt
.c06e	20 cf ff	smove   jsr input           ; chrin kernal call to input a character
.c071	9d 00 02	        sta inbuff,x        ; store in input buffer
.c074	e8		        inx
.c075	e0 28		        cpx #endin-inbuff   ; error if buffer is full
.c077	b0 1c		        bcs error
.c079	c9 0d		        cmp #$0d            ; keep reading until cr
.c07b	d0 f1		        bne smove
.c07d	a9 00		        lda #0              ; null-terminate input buffer
.c07f	9d ff 01	        sta inbuff-1,x      ; (replacing the cr)
.c082	20 23 c7	st1     jsr getchr          ; get a character from the buffer
.c085	f0 df		        beq strt            ; start over if buffer is empty
.c087	c9 20		        cmp #$20            ; skip leading spaces
.c089	f0 f7		        beq st1
.c08b	a2 16		s0      ldx #keytop-keyw    ; loop through valid command characters
.c08d	dd ac ca	s1      cmp keyw,x          ; see if input character matches
.c090	f0 0b		        beq s2              ; command matched, dispatch it
.c092	ca		        dex                 ; no match, check next command
.c093	10 f8		        bpl s1              ; keep trying until we've checked them all
.c095	a0 1e		error   ldy #msg3-msgbas    ; display "?" to indicate error and go to new line
.c097	20 89 c9	        jsr sndmsg
.c09a	4c 66 c0	        jmp strt            ; back to main loop
.c09d	e0 13		s2      cpx #$13            ; last 3 commands in table are load/save/validate
.c09f	b0 12		        bcs lsv             ;   which are handled by the same subroutine
.c0a1	e0 0f		        cpx #$0f            ; next 4 commands are base conversions
.c0a3	b0 14		        bcs cnvlnk          ;   which are handled by the same subroutine
.c0a5	8a		        txa                 ; remaining commands dispatch through vector table
.c0a6	0a		        asl a               ; multiply index of command by 2
.c0a7	aa		        tax                 ;   since table contains 2-byte addresses
.c0a8	bd c3 ca	        lda kaddr+1,x       ; push address from vector table onto stack
.c0ab	48		        pha                 ;   so that the rts from getpar will jump there
.c0ac	bd c2 ca	        lda kaddr,x
.c0af	48		        pha
.c0b0	4c 04 c6	        jmp getpar          ; get the first parameter for the command
.c0b3	8d 32 02	lsv     sta savy            ; handle load/save/validate
.c0b6	4c 86 c2	        jmp ld
.c0b9	4c c8 c7	cnvlnk  jmp convrt          ; handle base conversion
.c0bc	6c 02 a0	exit    jmp ($a002)         ; jump to warm-start vector to reinitialize basic
.c0bf	b0 08		dsplym  bcs dspm11          ; start from previous end addr if no address given
.c0c1	20 3d c7	        jsr copy12          ; save start address in tmp2
.c0c4	20 04 c6	        jsr getpar          ; get end address in tmp0
.c0c7	90 06		        bcc dsmnew          ; did user specify one?
.c0c9	a9 0b		dspm11  lda #$0b            ; if not, show 12 lines by default
.c0cb	85 c1		        sta tmp0
.c0cd	d0 0e		        bne dspbyt          ; always true, but bne uses 1 byte less than jmp
.c0cf	20 46 c7	dsmnew  jsr sub12           ; end addr given, calc bytes between start and end
.c0d2	90 1e		        bcc merror          ; error if start is after end
.c0d4	a2 03		        ldx #3              ; divide by 8 (shift right 3 times)
.c0d6	46 c2		dspm01  lsr tmp0+1
.c0d8	66 c1		        ror tmp0
.c0da	ca		        dex
.c0db	d0 f9		        bne dspm01
.c0dd	20 e1 ff	dspbyt  jsr stop            ; check for stop key
.c0e0	f0 0d		        beq dspmx           ; exit early if pressed
.c0e2	20 62 c1	        jsr dispmem         ; display 1 line containing 8 bytes
.c0e5	a9 08		        lda #8              ; increase start address by 8 bytes
.c0e7	20 7c c7	        jsr bumpad2
.c0ea	20 54 c7	        jsr suba1           ; decrement line counter
.c0ed	b0 ee		        bcs dspbyt          ; show another line until it's < 0
.c0ef	4c 66 c0	dspmx   jmp strt            ; back to main loop
.c0f2	4c 95 c0	merror  jmp error           ; handle error
.c0f5	20 94 c7	altr    jsr copy1p          ; store first parameter in pc
.c0f8	a0 00		        ldy #0              ; init counter
.c0fa	20 04 c6	altr1   jsr getpar          ; get value for next register
.c0fd	b0 0a		        bcs altrx           ; exit early if no more values given
.c0ff	a5 c1		        lda tmp0            ; store in memory, offset from sr
.c101	99 2a 02	        sta sr,y            ; these locations will be transferred to the
.c104	c8		        iny                 ;   actual registers before exiting the monitor
.c105	c0 05		        cpy #$05            ; have we updated all 5 yet?
.c107	90 f1		        bcc altr1           ; if not, get next
.c109	4c 66 c0	altrx   jmp strt            ; back to main loop
.c10c	b0 13		altm    bcs altmx           ; exit if no parameter provided
.c10e	20 3d c7	        jsr copy12          ; copy parameter to start address
.c111	a0 00		        ldy #0
.c113	20 04 c6	altm1   jsr getpar          ; get value for next byte of memory
.c116	b0 09		        bcs altmx           ; if none given, exit early
.c118	a5 c1		        lda tmp0            ; poke value into memory at start address + y
.c11a	91 c3		        sta (tmp2),y
.c11c	c8		        iny                 ; next byte
.c11d	c0 08		        cpy #8              ; have we read 8 bytes yet?
.c11f	90 f2		        bcc altm1           ; if not, read the next one
.c121	a9 91		altmx   lda #$91            ; move cursor up
.c123	20 d2 ff	        jsr chrout
.c126	20 62 c1	        jsr dispmem         ; re-display line to make ascii match hex
.c129	4c 66 c0	        jmp strt            ; back to main loop
.c12c	ae 2e 02	goto    ldx sp              ; load stack pointer from memory
.c12f	9a		        txs                 ; save in sp register
.c130	20 94 c7	goto2   jsr copy1p          ; copy provided address to pc
.c133	78		        sei                 ; disable interrupts
.c134	ad 28 02	        lda pch             ; push pc high byte on stack
.c137	48		        pha
.c138	ad 29 02	        lda pcl             ; push pc low byte on stack
.c13b	48		        pha
.c13c	ad 2a 02	        lda sr              ; push status byte on stack
.c13f	48		        pha
.c140	ad 2b 02	        lda acc             ; load accumulator from memory
.c143	ae 2c 02	        ldx xr              ; load x from memory
.c146	ac 2d 02	        ldy yr              ; load y from memory
.c149	40		        rti                 ; return from interrupt (pops pc and sr)
.c14a	ae 2e 02	jsub    ldx sp              ; load stack pointer from memory
.c14d	9a		        txs                 ; save value in sp register
.c14e	20 30 c1	        jsr goto2           ; same as goto command
.c151	8c 2d 02	        sty yr              ; save y to memory
.c154	8e 2c 02	        stx xr              ; save x to memory
.c157	8d 2b 02	        sta acc             ; save accumulator to memory
.c15a	08		        php                 ; push processor status on stack
.c15b	68		        pla                 ; pull processor status into a
.c15c	8d 2a 02	        sta sr              ; save processor status to memory
.c15f	4c 44 c0	        jmp dsplyr          ; display registers
.c162	20 e2 c6	dispmem jsr crlf            ; new line
.c165	a9 3e		        lda #">"            ; prefix > so memory can be edited in place
.c167	20 d2 ff	        jsr chrout
.c16a	20 cd c6	        jsr showad          ; show address of first byte on line
.c16d	a0 00		        ldy #0
.c16f	f0 03		        beq dmemgo          ; showad already printed a space after the address
.c171	20 da c6	dmemlp  jsr space           ; print space between bytes
.c174	b1 c3		dmemgo  lda (tmp2),y        ; load byte from start address + y
.c176	20 fb c6	        jsr wrtwo           ; output hex digits for byte
.c179	c8		        iny                 ; next byte
.c17a	c0 08		        cpy #8              ; have we output 8 bytes yet?
.c17c	90 f3		        bcc dmemlp          ; if not, output next byte
.c17e	a0 26		        ldy #msg5-msgbas    ; if so, output : and turn on reverse video
.c180	20 89 c9	        jsr sndmsg          ;   before displaying ascii representation
.c183	a0 00		        ldy #0              ; back to first byte in line
.c185	b1 c3		dchar   lda (tmp2),y        ; load byte at start address + y
.c187	aa		        tax                 ; stash in x
.c188	29 bf		        and #$bf            ; clear 6th bit
.c18a	c9 22		        cmp #$22            ; is it a quote (")?"
.c18c	f0 08		        beq ddot            ; if so, print . instead
.c18e	8a		        txa                 ; if not, restore character
.c18f	29 7f		        and #$7f            ; clear top bit
.c191	c9 20		        cmp #$20            ; is it a printable character (>= $20)?
.c193	8a		        txa                 ; restore character
.c194	b0 02		        bcs dchrok          ; if printable, output character
.c196	a9 2e		ddot    lda #$2e            ; if not, output '.' instaed
.c198	20 d2 ff	dchrok  jsr chrout          ; send it
.c19b	c8		        iny                 ; next byte
.c19c	c0 08		        cpy #8              ; have we output 8 bytes yet?
.c19e	90 e5		        bcc dchar           ; if not, output next byte
.c1a0	60		        rts
.c1a1	a9 00		compar  lda #0              ; bit 7 clear signals compare
>c1a3	2c		        .byte $2c           ; absolute bit opcode consumes next word (lda #$80)
.c1a4	a9 80		trans   lda #$80            ; bit 7 set signals transfer
.c1a6	8d 32 02	        sta savy            ; save compare/transfer flag in savy
.c1a9	a9 00		        lda #0              ; assume we're counting up (bit 7 clear)
.c1ab	8d 07 01	        sta upflg           ; save direction flag
.c1ae	20 a1 c7	        jsr getdif          ; get two addresses and calculate difference
.c1b1	b0 05		        bcs terror          ; carry set indicates error
.c1b3	20 04 c6	        jsr getpar          ; get destination address in tmp0
.c1b6	90 03		        bcc tokay           ; carry set indicates error
.c1b8	4c 95 c0	terror  jmp error           ; handle error
.c1bb	2c 32 02	tokay   bit savy            ; transfer or compare?
.c1be	10 27		        bpl compar1         ; high bit clear indicates compare
.c1c0	a5 c3		        lda tmp2            ; if it's a transfer, we must take steps
.c1c2	c5 c1		        cmp tmp0            ;   to avoid overwriting the source bytes before
.c1c4	a5 c4		        lda tmp2+1          ;   they have been transferred
.c1c6	e5 c2		        sbc tmp0+1          ; compare source (tmp2) to destination (tmp0)
.c1c8	b0 1d		        bcs compar1         ; and count up if source is before than desitnation
.c1ca	ad 2f 02	        lda store           ; otherwise, start at end and count down...
.c1cd	65 c1		        adc tmp0            ; add length (store) to desintation (tmp0)
.c1cf	85 c1		        sta tmp0            ; to calculate end of destination
.c1d1	ad 30 02	        lda store+1
.c1d4	65 c2		        adc tmp0+1
.c1d6	85 c2		        sta tmp0+1
.c1d8	a2 01		        ldx #1              ; change source pointer from beginning to end
.c1da	bd 0b 01	tdown   lda stash,x         ; tmp2 = source end (stash)
.c1dd	95 c3		        sta tmp2,x
.c1df	ca		        dex
.c1e0	10 f8		        bpl tdown
.c1e2	a9 80		        lda #$80            ; high bit set in upflg means count down
.c1e4	8d 07 01	        sta upflg
.c1e7	20 e2 c6	compar1 jsr crlf            ; new line
.c1ea	a0 00		        ldy #0              ; no offset from pointer
.c1ec	20 e1 ff	tcloop  jsr stop            ; check for stop key
.c1ef	f0 31		        beq texit           ; exit if pressed
.c1f1	b1 c3		        lda (tmp2),y        ; load byte from source
.c1f3	2c 32 02	        bit savy            ; transfer or compare?
.c1f6	10 02		        bpl compar2         ; skip store if comparing
.c1f8	91 c1		        sta (tmp0),y        ; otherwise, store in destination
.c1fa	d1 c1		compar2 cmp (tmp0),y        ; compare to destination
.c1fc	f0 03		        beq tmvad           ; don't show address if equal
.c1fe	20 cd c6	        jsr showad          ; show address
.c201	2c 07 01	tmvad   bit upflg           ; counting up or down?
.c204	30 0b		        bmi tdecad          ; high bit set means we're counting down
.c206	e6 c1		        inc tmp0            ; increment destination low byte
.c208	d0 10		        bne tincok
.c20a	e6 c2		        inc tmp0+1          ; carry to high byte if necessary
.c20c	d0 0c		        bne tincok
.c20e	4c 95 c0	        jmp error           ; error if high byte overflowed
.c211	20 54 c7	tdecad  jsr suba1           ; decrement destination (tmp0)
.c214	20 86 c7	        jsr sub21           ; decrement source (tmp2)
.c217	4c 1d c2	        jmp tmor
.c21a	20 7a c7	tincok  jsr adda2           ; increment source (tmp2)
.c21d	20 68 c7	tmor    jsr sub13           ; decrement length
.c220	b0 ca		        bcs tcloop          ; loop until length is 0
.c222	4c 66 c0	texit   jmp strt            ; back to main loop
.c225	20 a1 c7	hunt    jsr getdif          ; get start (tmp2) and end (tmp0) of haystack
.c228	b0 59		        bcs herror          ; carry indicates error
.c22a	a0 00		        ldy #0
.c22c	20 23 c7	        jsr getchr          ; get a single character
.c22f	c9 27		        cmp #"'"            ; is it a single quote?
.c231	d0 16		        bne nostrh          ; if not, input needle as hex bytes
.c233	20 23 c7	        jsr getchr          ; if so, input needle as string
.c236	c9 00		        cmp #0
.c238	f0 49		        beq herror          ; error if needle isn't at least one byte
.c23a	99 17 01	hpar    sta stage,y         ; save char in staging area
.c23d	c8		        iny
.c23e	20 23 c7	        jsr getchr          ; get another char
.c241	f0 18		        beq htgo            ; if it's null start searching
.c243	c0 1e		        cpy #estage-stage   ; have we filled up the needle staging area?
.c245	d0 f3		        bne hpar            ; if not, get another character
.c247	f0 12		        beq htgo            ; if so, start searching
.c249	20 01 c6	nostrh  jsr rdpar           ; read hex bytes if string not indicated
.c24c	a5 c1		hlp     lda tmp0            ; save last read byte in staging area
.c24e	99 17 01	        sta stage,y
.c251	c8		        iny                 ; get another hex byte
.c252	20 04 c6	        jsr getpar
.c255	b0 04		        bcs htgo            ; if there is none, start searching
.c257	c0 1e		        cpy #estage-stage   ; have we filled up the needle staging area?
.c259	d0 f1		        bne hlp             ; if not, get another byte
.c25b	8c 32 02	htgo    sty savy            ; save length of needle
.c25e	20 e2 c6	        jsr crlf            ; new line
.c261	a0 00		hscan   ldy #0
.c263	b1 c3		hlp3    lda (tmp2),y        ; get first byte in haystack
.c265	d9 17 01	        cmp stage,y         ; compare it to first byte of needle
.c268	d0 09		        bne hnoft           ; if it doesn't match, we haven't found anything
.c26a	c8		        iny                 ; if it does, check the next byte
.c26b	cc 32 02	        cpy savy            ; have we reached the end of the needle?
.c26e	d0 f3		        bne hlp3            ; if not, keep comparing bytes
.c270	20 cd c6	        jsr showad          ; match found, show address
.c273	20 e1 ff	hnoft   jsr stop            ; no match, check for stop key
.c276	f0 08		        beq hexit           ; exit prematurely if pressed
.c278	20 7a c7	        jsr adda2           ; increment haystack pointer
.c27b	20 68 c7	        jsr sub13           ; decrement haystack length
.c27e	b0 e1		        bcs hscan           ; still more haystack? keep searching
.c280	4c 66 c0	hexit   jmp strt            ; back to main loop
.c283	4c 95 c0	herror  jmp error           ; handle error
.c286	a0 01		ld      ldy #1              ; default to reading from tape, device #1
.c288	84 ba		        sty fa
.c28a	84 b9		        sty sadd            ; default to secondary address #1
.c28c	88		        dey
.c28d	84 b7		        sty fnlen           ; start with an empty filename
.c28f	84 90		        sty satus           ; clear status
.c291	a9 01		        lda #>stage         ; set filename pointer to staging buffer
.c293	85 bc		        sta fnadr+1
.c295	a9 17		        lda #<stage
.c297	85 bb		        sta fnadr
.c299	20 23 c7	l1      jsr getchr          ; get a character
.c29c	f0 59		        beq lshort          ; no filename given, try load or verify from tape
.c29e	c9 20		        cmp #$20            ; skip leading spaces
.c2a0	f0 f7		        beq l1
.c2a2	c9 22		        cmp #$22            ; error if filename doesn't start with a quote
.c2a4	d0 16		        bne lerror
.c2a6	ae 31 02	        ldx chrpnt          ; load current char pointer into index reg
.c2a9	bd 00 02	l3      lda inbuff,x        ; load current char from buffer to accumulator
.c2ac	f0 49		        beq lshort          ; no filename given, try load or verify from tape
.c2ae	e8		        inx                 ; next char
.c2af	c9 22		        cmp #$22            ; is it a quote?
.c2b1	f0 0c		        beq l8              ; if so, we've reached the end of the filename
.c2b3	91 bb		        sta (fnadr),y       ; if not, save character in filename buffer
.c2b5	e6 b7		        inc fnlen           ; increment filename length
.c2b7	c8		        iny
.c2b8	c0 1e		        cpy #estage-stage   ; check whether buffer is full
.c2ba	90 ed		        bcc l3              ; if not, get another character
.c2bc	4c 95 c0	lerror  jmp error           ; if so, handle error
.c2bf	8e 31 02	l8      stx chrpnt          ; set character pointer to the current index
.c2c2	20 23 c7	        jsr getchr          ; eat separator between filename and device #
.c2c5	f0 30		        beq lshort          ; no separator, try to load or verify from tape
.c2c7	20 04 c6	        jsr getpar          ; get device number
.c2ca	b0 2b		        bcs lshort          ; no device # given, try load or verify from tape
.c2cc	a5 c1		        lda tmp0            ; set device number for kernal routines
.c2ce	85 ba		        sta fa
.c2d0	20 04 c6	        jsr getpar          ; get start address for load or save in tmp0
.c2d3	b0 22		        bcs lshort          ; no start address, try to load or verify
.c2d5	20 3d c7	        jsr copy12          ; transfer start address to tmp2
.c2d8	20 04 c6	        jsr getpar          ; get end address for save in tmp0
.c2db	b0 3d		        bcs ldaddr          ; no end address, try to load to given start addr
.c2dd	20 e2 c6	        jsr crlf            ; new line
.c2e0	a6 c1		        ldx tmp0            ; put low byte of end address in x
.c2e2	a4 c2		        ldy tmp0+1          ; put high byte of end address in y
.c2e4	ad 32 02	        lda savy            ; confirm that we're doing a save
.c2e7	c9 53		        cmp #"s"
.c2e9	d0 d1		        bne lerror          ; if not, error due to too many params
.c2eb	a9 00		        lda #0
.c2ed	85 b9		        sta sadd            ; set secondary address to 0
.c2ef	a9 c3		        lda #tmp2           ; put addr of zero-page pointer to data in a
.c2f1	20 d8 ff	        jsr save            ; call kernal save routine
.c2f4	4c 66 c0	lsvxit  jmp strt            ; back to mainloop
.c2f7	ad 32 02	lshort  lda savy            ; check which command we received
.c2fa	c9 56		        cmp #"v"
.c2fc	f0 06		        beq loadit          ; we're doing a verify so don't set a to 0
.c2fe	c9 4c		        cmp #"l"
.c300	d0 ba		        bne lerror          ; error due to not enough params for save
.c302	a9 00		        lda #0              ; 0 in a signals load, anything else is verify
.c304	20 d5 ff	loadit  jsr load            ; call kernal load routine
.c307	a5 90		        lda satus           ; get i/o status
.c309	29 10		        and #$10            ; check bit 5 for checksum error
.c30b	f0 e7		        beq lsvxit          ; if no error go back to mainloop
.c30d	ad 32 02	        lda savy            ; ?? not sure what these two lines are for...
.c310	f0 aa		        beq lerror          ; ?? savy will never be 0, so why check?
.c312	a0 28		        ldy #msg6-msgbas    ; display "error" if checksum didn't match
.c314	20 89 c9	        jsr sndmsg
.c317	4c 66 c0	        jmp strt            ; back to mainloop
.c31a	a6 c3		ldaddr  ldx tmp2            ; load address low byte in x
.c31c	a4 c4		        ldy tmp2+1          ; load address high byte in y
.c31e	a9 00		        lda #0              ; 0 in a signals load
.c320	85 b9		        sta sadd            ; secondary addr 0 means load to addr in x and y
.c322	f0 d3		        beq lshort          ; execute load
.c324	20 a1 c7	fill    jsr getdif          ; start in tmp2, end in stash, length in store
.c327	b0 59		        bcs aerror          ; carry set indicates error
.c329	20 04 c6	        jsr getpar          ; get value to fill in tmp0
.c32c	b0 54		        bcs aerror          ; carry set indicates error
.c32e	20 23 c7	        jsr getchr          ; any more characters triggers an error
.c331	d0 4f		        bne aerror
.c333	a0 00		        ldy #0              ; no offset
.c335	a5 c1		fillp   lda tmp0            ; load value to fill in accumulator
.c337	91 c3		        sta (tmp2),y        ; store fill value in current address
.c339	20 e1 ff	        jsr stop            ; check for stop key
.c33c	f0 08		        beq fstart          ; if pressed, back to main loop
.c33e	20 7a c7	        jsr adda2           ; increment address
.c341	20 68 c7	        jsr sub13           ; decrement length
.c344	b0 ef		        bcs fillp           ; keep going until length reaches 0
.c346	4c 66 c0	fstart  jmp strt            ; back to main loop
.c349	b0 37		assem   bcs aerror          ; error if no address given
.c34b	20 3d c7	        jsr copy12          ; copy address to tmp2
.c34e	a2 00		aget1   ldx #0
.c350	8e 0e 01	        stx u0aa0+1         ; clear byte that mnemonic gets shifted into
.c353	8e 08 01	        stx digcnt          ; clear digit count
.c356	20 23 c7	aget2   jsr getchr          ; get a char
.c359	d0 04		        bne almor           ; proceed if the character isn't null
.c35b	e0 00		        cpx #0              ; it's null, have read a mnemonic yet?
.c35d	f0 e7		        beq fstart          ; if not, silently go back to main loop
.c35f	c9 20		almor   cmp #$20            ; skip leading spaces
.c361	f0 eb		        beq aget1
.c363	9d 02 01	        sta mnemw,x         ; put character in mnemonic buffer
.c366	e8		        inx
.c367	e0 03		        cpx #3              ; have we read 3 characters yet?
.c369	d0 eb		        bne aget2           ; if not, get next character
.c36b	ca		asqeez  dex                 ; move to previous char
.c36c	30 17		        bmi aoprnd          ; if we're done with mnemonic, look for operand
.c36e	bd 02 01	        lda mnemw,x         ; get current character
.c371	38		        sec                 ; pack 3-letter mnemonic into 2 bytes (15 bits)
.c372	e9 3f		        sbc #$3f            ; subtract $3f from ascii code so a-z = 2 to 27
.c374	a0 05		        ldy #$05            ; letters now fit in 5 bits; shift them out
.c376	4a		ashift  lsr a               ;   into the first two bytes of the inst buffer
.c377	6e 0e 01	        ror u0aa0+1         ; catch the low bit from accumulator in right byte
.c37a	6e 0d 01	        ror u0aa0           ; catch the low bit from right byte in left byte
.c37d	88		        dey                 ; count down bits
.c37e	d0 f6		        bne ashift          ; keep looping until we reach zero
.c380	f0 e9		        beq asqeez          ; unconditional branch to handle next char
.c382	4c 95 c0	aerror  jmp error           ; handle error
.c385	a2 02		aoprnd  ldx #2              ; mnemonic is in first two bytes so start at third
.c387	ad 08 01	ascan   lda digcnt          ; did we find address digits last time?
.c38a	d0 2d		        bne aform1          ; if so, look for mode chars
.c38c	20 2c c6	        jsr rdval           ; otherwise, look for an address
.c38f	f0 25		        beq aform0          ; we didn't find an address, look for characters
.c391	b0 ef		        bcs aerror          ; carry flag indicates error
.c393	a9 24		        lda #"$"
.c395	9d 0d 01	        sta u0aa0,x         ; prefix addresses with $
.c398	e8		        inx                 ; next position in buffer
.c399	a0 04		        ldy #4              ; non-zero page addresses are 4 hex digits
.c39b	ad 0a 01	        lda numbit          ; check numeric base in which address was given
.c39e	c9 08		        cmp #8              ; for addresses given in octal or binary
.c3a0	90 05		        bcc aaddr           ;   use only the high byte to determine page
.c3a2	cc 08 01	        cpy digcnt          ; for decimal or hex, force non-zero page addressing
.c3a5	f0 06		        beq afill0          ;   if address was given with four digits or more
.c3a7	a5 c2		aaddr   lda tmp0+1          ; check whether high byte of address is zero
.c3a9	d0 02		        bne afill0          ; non-zero high byte means we're not in zero page
.c3ab	a0 02		        ldy #2              ; if it's in zero page, addr is 2 hex digits
.c3ad	a9 30		afill0  lda #$30            ; use 0 as placeholder for each hex digit in addr
.c3af	9d 0d 01	afil0l  sta u0aa0,x         ; put placeholder in assembly buffer
.c3b2	e8		        inx                 ; move to next byte in buffer
.c3b3	88		        dey                 ; decrement number of remaining digits
.c3b4	d0 f9		        bne afil0l          ; loop until all digits have been placed
.c3b6	ce 31 02	aform0  dec chrpnt          ; non-numeric input; back 1 char to see what it was
.c3b9	20 23 c7	aform1  jsr getchr          ; get next character
.c3bc	f0 0e		        beq aescan          ; if there is none, we're finished scanning
.c3be	c9 20		        cmp #$20            ; skip spaces
.c3c0	f0 c5		        beq ascan
.c3c2	9d 0d 01	        sta u0aa0,x         ; store character in assembly buffer
.c3c5	e8		        inx                 ; move to next byte in buffer
.c3c6	e0 0a		        cpx #u0aae-u0aa0    ; is instruction buffer full?
.c3c8	90 bd		        bcc ascan           ; if not, keep scanning
.c3ca	b0 b6		        bcs aerror          ; error if buffer is full
.c3cc	8e 2f 02	aescan  stx store           ; save number of bytes in assembly buffer
.c3cf	a2 00		        ldx #0              ; start at opcode $00 and check every one until
.c3d1	8e 06 01	        stx opcode          ;   we find one that matches our criteria
.c3d4	a2 00		atryop  ldx #0
.c3d6	8e 33 02	        stx u9f             ; reset index into work buffer
.c3d9	ad 06 01	        lda opcode
.c3dc	20 93 c5	        jsr instxx          ; look up instruction format for current opcode
.c3df	ae 00 01	        ldx acmd            ; save addressing command for later
.c3e2	8e 30 02	        stx store+1
.c3e5	aa		        tax                 ; use current opcode as index
.c3e6	bd 68 ca	        lda mnemr,x         ; check right byte of compressed mnemonic
.c3e9	20 ba c4	        jsr chekop
.c3ec	bd 28 ca	        lda mneml,x         ; check left byte of compressed mnemonic
.c3ef	20 ba c4	        jsr chekop
.c3f2	a2 06		        ldx #6              ; 6 possible characters to check against operand
.c3f4	e0 03		tryit   cpx #3              ; are we on character 3?
.c3f6	d0 14		        bne trymod          ; if not, check operand characters
.c3f8	ac 01 01	        ldy length          ; otherwise, check number of bytes in operand
.c3fb	f0 0f		        beq trymod          ; if zero, check operand characters
.c3fd	ad 00 01	tryad   lda acmd            ; otherwise, look for an address
.c400	c9 e8		        cmp #$e8            ; special case for relative addressing mode
.c402	a9 30		        lda #$30            ; '0' is the digit placeholder we're looking for
.c404	b0 1e		        bcs try4b           ; acmd >= $e8 indicates relative addressing
.c406	20 b7 c4	        jsr chek2b          ; acmd < $e8 indicates normal addressing
.c409	88		        dey                 ; consume byte
.c40a	d0 f1		        bne tryad           ; check for 2 more digits if not zero-page
.c40c	0e 00 01	trymod  asl acmd            ; shift a bit out of the addressing command
.c40f	90 0e		        bcc ub4df           ; if it's zero, skip checking current character
.c411	bd 1b ca	        lda char1-1,x
.c414	20 ba c4	        jsr chekop          ; otherwise first character against operand
.c417	bd 21 ca	        lda char2-1,x       ; get second character to check
.c41a	f0 03		        beq ub4df           ; if it's zero, skip checking it
.c41c	20 ba c4	        jsr chekop          ; otherwise check it against hte operand
.c41f	ca		ub4df   dex                 ; move to next character
.c420	d0 d2		        bne tryit           ; repeat tests
.c422	f0 06		        beq trybran
.c424	20 b7 c4	try4b   jsr chek2b          ; check for 4 digit address placeholder
.c427	20 b7 c4	        jsr chek2b          ;   by checking for 2 digits twice
.c42a	ad 2f 02	trybran lda store           ; get number of bytes in assembly buffer
.c42d	cd 33 02	        cmp u9f             ; more bytes left to check?
.c430	f0 03		        beq abran           ; if not, we've found a match; build instruction
.c432	4c c7 c4	        jmp bumpop          ; if so, this opcode doesn't match; try the next
.c435	ac 01 01	abran   ldy length          ; get number of bytes in operand
.c438	f0 32		        beq a1byte          ; if none, just output the opcode
.c43a	ad 30 02	        lda store+1         ; otherwise check the address format
.c43d	c9 9d		        cmp #$9d            ; is it a relative branch?
.c43f	d0 23		        bne objput          ; if not, skip relative branch calculation
.c441	a5 c1		        lda tmp0            ; calculate the difference between the current
.c443	e5 c3		        sbc tmp2            ;   address and the branch target (low byte)
.c445	aa		        tax                 ; save it in x
.c446	a5 c2		        lda tmp0+1          ; borrow from the high byte if necessary
.c448	e5 c4		        sbc tmp2+1
.c44a	90 08		        bcc abback          ; if result is negative, we're branching back
.c44c	d0 66		        bne serror          ; high bytes must be equal when branching forward
.c44e	e0 82		        cpx #$82            ; difference between low bytes must be < 130
.c450	b0 62		        bcs serror          ; error if the address is too far away
.c452	90 08		        bcc abranx
.c454	a8		abback  tay                 ; when branching backward high byte of target must
.c455	c8		        iny                 ;   be 1 less than high byte of current address
.c456	d0 5c		        bne serror          ; if not, it's too far away
.c458	e0 82		        cpx #$82            ; difference between low bytes must be < 130
.c45a	90 58		        bcc serror          ; if not, it's too far away
.c45c	ca		abranx  dex                 ; adjust branch target relative to the
.c45d	ca		        dex                 ;   instruction following this one
.c45e	8a		        txa
.c45f	ac 01 01	        ldy length          ; load length of operand
.c462	d0 03		        bne objp2           ; don't use the absolute address
.c464	b9 c0 00	objput  lda tmp0-1,y        ; get the operand
.c467	91 c3		objp2   sta (tmp2),y        ; store it after the opcode
.c469	88		        dey
.c46a	d0 f8		        bne objput          ; copy the other byte of operand if there is one
.c46c	ad 06 01	a1byte  lda opcode          ; put opcode into instruction
.c46f	91 c3		        sta (tmp2),y
.c471	20 e2 c6	        jsr crlf            ; carriage return
.c474	a9 91		        lda #$91            ; back up one line
.c476	20 d2 ff	        jsr chrout
.c479	a0 2e		        ldy #msg7-msgbas    ; "a " prefix
.c47b	20 76 c9	        jsr sndclr          ; clear line
.c47e	20 15 c5	        jsr dislin          ; disassemble the instruction we just assembled
.c481	ee 01 01	        inc length          ; instruction length = operand length + 1 byte
.c484	ad 01 01	        lda length          ;   for the opcode
.c487	20 7c c7	        jsr bumpad2         ; increment address by length of instruction
.c48a	a9 41		        lda #"a"            ; stuff keyboard buffer with next assemble command:
.c48c	8d 77 02	        sta keyd            ;   "a xxxx " where xxxx is the next address
.c48f	a9 20		        lda #" "            ;   after the previously assembled instruction
.c491	8d 78 02	        sta keyd+1
.c494	8d 7d 02	        sta keyd+6
.c497	a5 c4		        lda tmp2+1          ; convert high byte of next address to hex
.c499	20 0b c7	        jsr asctwo
.c49c	8d 79 02	        sta keyd+2          ; put it in the keyboard buffer
.c49f	8e 7a 02	        stx keyd+3
.c4a2	a5 c3		        lda tmp2            ; convert low byte of next address to hex
.c4a4	20 0b c7	        jsr asctwo
.c4a7	8d 7b 02	        sta keyd+4          ; put it in the keyboard buffer
.c4aa	8e 7c 02	        stx keyd+5
.c4ad	a9 07		        lda #7              ; set number of chars in keyboard buffer
.c4af	85 c6		        sta ndx
.c4b1	4c 66 c0	        jmp strt            ; back to main loop
.c4b4	4c 95 c0	serror  jmp error           ; handle error
.c4b7	20 ba c4	chek2b  jsr chekop          ; check two bytes against value in accumulator
.c4ba	8e 05 01	chekop  stx savx            ; stash x
.c4bd	ae 33 02	        ldx u9f             ; get current index into work buffer
.c4c0	dd 0d 01	        cmp u0aa0,x         ; check whether this opcode matches the buffer
.c4c3	f0 0a		        beq opok            ;   matching so far, check the next criteria
.c4c5	68		        pla                 ; didn't match, so throw away return address
.c4c6	68		        pla                 ;   on the stack because we're starting over
.c4c7	ee 06 01	bumpop  inc opcode          ; check the next opcode
.c4ca	f0 e8		        beq serror          ; error if we tried every opcode and none fit
.c4cc	4c d4 c3	        jmp atryop          ; start over with new opcode
.c4cf	ee 33 02	opok    inc u9f             ; opcode matches so far; check the next criteria
.c4d2	ae 05 01	        ldx savx            ; restore x
.c4d5	60		        rts
.c4d6	b0 08		disass  bcs dis0ad          ; if no address was given, start from last address
.c4d8	20 3d c7	        jsr copy12          ; copy start address to tmp2
.c4db	20 04 c6	        jsr getpar          ; get end address in tmp0
.c4de	90 06		        bcc dis2ad          ; if one was given, skip default
.c4e0	a9 14		dis0ad  lda #$14            ; disassemble 14 bytes by default
.c4e2	85 c1		        sta tmp0            ; store length in tmp0
.c4e4	d0 05		        bne disgo           ; skip length calculation
.c4e6	20 46 c7	dis2ad  jsr sub12           ; calculate number of bytes between start and end
.c4e9	90 1f		        bcc derror          ; error if end address is before start address
.c4eb	20 70 c9	disgo   jsr cline           ; clear the current line
.c4ee	20 e1 ff	        jsr stop            ; check for stop key
.c4f1	f0 14		        beq disexit         ; exit early if pressed
.c4f3	20 0d c5	        jsr dsout1          ; output disassembly prefix ". "
.c4f6	ee 01 01	        inc length
.c4f9	ad 01 01	        lda length          ; add length of last instruction to start address
.c4fc	20 7c c7	        jsr bumpad2
.c4ff	ad 01 01	        lda length          ; subtract length of last inst from end address
.c502	20 56 c7	        jsr suba2
.c505	b0 e4		        bcs disgo
.c507	4c 66 c0	disexit jmp strt            ; back to mainloop
.c50a	4c 95 c0	derror  jmp error
.c50d	a9 2e		dsout1  lda #"."            ; output ". " prefix to allow edit and reassemble
.c50f	20 d2 ff	        jsr chrout
.c512	20 da c6	        jsr space
.c515	20 cd c6	dislin  jsr showad          ; show the address of the instruction
.c518	20 da c6	        jsr space           ; insert a space
.c51b	a0 00		        ldy #0              ; no offset
.c51d	b1 c3		        lda (tmp2),y        ; load operand of current instruction
.c51f	20 93 c5	        jsr instxx          ; get mnemonic and addressing mode for opcode
.c522	48		        pha                 ; save index into mnemonic table
.c523	ae 01 01	        ldx length          ; get length of operand
.c526	e8		        inx                 ; add 1 byte for opcode
.c527	ca		dsbyt   dex                 ; decrement index
.c528	10 0e		        bpl dshex           ; show hex for byte being disassembled
.c52a	8c 32 02	        sty savy            ; save index
.c52d	a0 30		        ldy #msg8-msgbas    ; skip 3 spaces
.c52f	20 89 c9	        jsr sndmsg
.c532	ac 32 02	        ldy savy            ; restore index
.c535	4c 3d c5	        jmp nxbyt
.c538	b1 c3		dshex   lda (tmp2),y        ; show hex for byte
.c53a	20 d7 c6	        jsr wrbyte
.c53d	c8		nxbyt   iny                 ; next byte
.c53e	c0 03		        cpy #3              ; have we output 3 bytes yet?
.c540	90 e5		        bcc dsbyt           ; if not, loop
.c542	68		        pla                 ; restore index into mnemonic table
.c543	a2 03		        ldx #3              ; 3 letters in mnemonic
.c545	20 db c5	        jsr propxx          ; print mnemonic
.c548	a2 06		        ldx #6              ; 6 possible address mode character combos
.c54a	e0 03		pradr1  cpx #3              ; have we checked the third combo yet?
.c54c	d0 16		        bne pradr3          ; if so, output the leading characters
.c54e	ac 01 01	        ldy length          ; get the length of the operand
.c551	f0 11		        beq pradr3          ; if it's zero, there's no operand to print
.c553	ad 00 01	pradr2  lda acmd            ; otherwise, get the addressing mode
.c556	c9 e8		        cmp #$e8            ; check for relative addressing
.c558	08		        php                 ; save result of check
.c559	b1 c3		        lda (tmp2),y        ; get the operand
.c55b	28		        plp                 ; restore result of check
.c55c	b0 1d		        bcs relad           ; handle a relative address
.c55e	20 fb c6	        jsr wrtwo           ; output digits from address
.c561	88		        dey
.c562	d0 ef		        bne pradr2          ; repeat for next byte of operand, if there is one
.c564	0e 00 01	pradr3  asl acmd            ; check whether addr mode uses the current char
.c567	90 0e		        bcc pradr4          ; if not, skip it
.c569	bd 1b ca	        lda char1-1,x       ; look up the first char in the table
.c56c	20 d2 ff	        jsr chrout          ; print first char
.c56f	bd 21 ca	        lda char2-1,x       ; look up the second char in the table
.c572	f0 03		        beq pradr4          ; if there's no second character, skip it
.c574	20 d2 ff	        jsr chrout          ; print second char
.c577	ca		pradr4  dex                 ; next potential address mode character
.c578	d0 d0		        bne pradr1          ; loop if we haven't checked them all yet
.c57a	60		        rts                 ; back to caller
.c57b	20 87 c5	relad   jsr ub64d           ; calculate absolute address from relative
.c57e	18		        clc
.c57f	69 01		        adc #1              ; adjust address relative to next instruction
.c581	d0 01		        bne relend          ; don't increment high byte unless we overflowed
.c583	e8		        inx                 ; increment high byte
.c584	4c d1 c6	relend  jmp wraddr          ; print address
.c587	a6 c4		ub64d   ldx tmp2+1          ; get high byte of current address
.c589	a8		        tay                 ; is relative address positive or negative?
.c58a	10 01		        bpl relc2           ; if positive, leave high byte alone
.c58c	ca		        dex                 ; if negative, decrement high byte
.c58d	65 c3		relc2   adc tmp2            ; add relative address to low byte
.c58f	90 01		        bcc relc3           ; if there's no carry, we're done
.c591	e8		        inx                 ; if there's a carry, increment the high byte
.c592	60		relc3   rts
.c593	a8		instxx  tay                 ; stash opcode in accumulator in y for later
.c594	4a		        lsr a               ; is opcode even or odd?
.c595	90 0b		        bcc ieven
.c597	4a		        lsr a
.c598	b0 17		        bcs err             ; invalid opcodes xxxxxx11
.c59a	c9 22		        cmp #$22
.c59c	f0 13		        beq err             ; invalid opcode 10001001
.c59e	29 07		        and #$07            ; mask bits to 10000xxx
.c5a0	09 80		        ora #$80
.c5a2	4a		ieven   lsr a               ; lsb determines whether to use left/right nybble
.c5a3	aa		        tax                 ; get format index using remaining high bytes
.c5a4	bd ca c9	        lda mode,x
.c5a7	b0 04		        bcs rtmode          ; look at left or right nybble based on carry bit
.c5a9	4a		        lsr a               ; if carry = 0, use left nybble
.c5aa	4a		        lsr a
.c5ab	4a		        lsr a
.c5ac	4a		        lsr a
.c5ad	29 0f		rtmode  and #$0f            ; if carry = 1, use right nybble
.c5af	d0 04		        bne getfmt
.c5b1	a0 80		err     ldy #$80            ; substitute 10000000 for invalid opcodes
.c5b3	a9 00		        lda #0
.c5b5	aa		getfmt  tax
.c5b6	bd 0e ca	        lda mode2,x         ; lookup operand format using selected nybble
.c5b9	8d 00 01	        sta acmd            ; save for later use
.c5bc	29 03		        and #$03            ; lower 2 bits indicate number of bytes in operand
.c5be	8d 01 01	        sta length
.c5c1	98		        tya                 ; restore original opcode
.c5c2	29 8f		        and #$8f            ; mask bits to x000xxxx
.c5c4	aa		        tax                 ; save it
.c5c5	98		        tya                 ; restore original opcode
.c5c6	a0 03		        ldy #3
.c5c8	e0 8a		        cpx #$8a            ; check if opcode = 1xxx1010
.c5ca	f0 0b		        beq gtfm4
.c5cc	4a		gtfm2   lsr a               ; transform opcode into index for mnemonic table
.c5cd	90 08		        bcc gtfm4
.c5cf	4a		        lsr a               ; opcodes transformed as follows:
.c5d0	4a		gtfm3   lsr a               ; 1xxx1010->00101xxx
.c5d1	09 20		        ora #$20            ; xxxyyy01->00111xxx
.c5d3	88		        dey                 ; xxxyyy10->00111xxx
.c5d4	d0 fa		        bne gtfm3           ; xxxyy100->00110xxx
.c5d6	c8		        iny                 ; xxxxx000->000xxxxx
.c5d7	88		gtfm4   dey
.c5d8	d0 f2		        bne gtfm2
.c5da	60		        rts
.c5db	a8		propxx  tay                 ; use index in accumulator to look up mnemonic
.c5dc	b9 28 ca	        lda mneml,y         ;   and place a temporary copy in store
.c5df	8d 2f 02	        sta store
.c5e2	b9 68 ca	        lda mnemr,y
.c5e5	8d 30 02	        sta store+1
.c5e8	a9 00		prmn1   lda #0              ; clear accumulator
.c5ea	a0 05		        ldy #$05            ; shift 5 times
.c5ec	0e 30 02	prmn2   asl store+1         ; shift right byte
.c5ef	2e 2f 02	        rol store           ; rotate bits from right byte into left byte
.c5f2	2a		        rol a               ; rotate bits from left byte into accumulator
.c5f3	88		        dey                 ; next bit
.c5f4	d0 f6		        bne prmn2           ; loop until all bits shifted
.c5f6	69 3f		        adc #$3f            ; calculate ascii code for letter by adding to '?'
.c5f8	20 d2 ff	        jsr chrout          ; output letter
.c5fb	ca		        dex                 ; next letter
.c5fc	d0 ea		        bne prmn1           ; loop until all 3 letters are output
.c5fe	4c da c6	        jmp space           ; output space
.c601	ce 31 02	rdpar   dec chrpnt          ; back up one char
.c604	20 2c c6	getpar  jsr rdval           ; read the value
.c607	b0 17		        bcs gterr           ; carry set indicates error
.c609	20 20 c7	        jsr gotchr          ; check previous character
.c60c	d0 0a		        bne ckterm          ; if it's not null, check if it's a valid separator
.c60e	ce 31 02	        dec chrpnt          ; back up one char
.c611	ad 08 01	        lda digcnt          ; get number of digits read
.c614	d0 11		        bne getgot          ; found some digits
.c616	f0 0d		        beq gtnil           ; didn't find any digits
.c618	c9 20		ckterm  cmp #$20            ; space or comma are valid separators
.c61a	f0 0b		        beq getgot          ; anything else is an error
.c61c	c9 2c		        cmp #","
.c61e	f0 07		        beq getgot
.c620	68		gterr   pla                 ; encountered error
.c621	68		        pla                 ; get rid of command vector pushed on stack
.c622	4c 95 c0	        jmp error           ; handle error
.c625	38		gtnil   sec                 ; set carry to indicate no parameter found
>c626	24		        .byte $24           ; bit zp opcode consumes next byte (clc)
.c627	18		getgot  clc                 ; clear carry to indicate paremeter returned
.c628	ad 08 01	        lda digcnt          ; return number of digits in a
.c62b	60		        rts                 ; return to address pushed from vector table
.c62c	a9 00		rdval   lda #0              ; clear temp
.c62e	85 c1		        sta tmp0
.c630	85 c2		        sta tmp0+1
.c632	8d 08 01	        sta digcnt          ; clear digit counter
.c635	8a		        txa                 ; save x and y
.c636	48		        pha
.c637	98		        tya
.c638	48		        pha
.c639	20 23 c7	rdvmor  jsr getchr          ; get next character from input buffer
.c63c	f0 1e		        beq rdnilk          ; null at end of buffer
.c63e	c9 20		        cmp #$20            ; skip spaces
.c640	f0 f7		        beq rdvmor
.c642	a2 03		        ldx #3              ; check numeric base [$+&%]
.c644	dd bb ca	gnmode  cmp hikey,x
.c647	f0 07		        beq gotmod          ; got a match, set up base
.c649	ca		        dex
.c64a	10 f8		        bpl gnmode          ; check next base
.c64c	e8		        inx                 ; default to hex
.c64d	ce 31 02	        dec chrpnt          ; back up one character
.c650	bc e0 ca	gotmod  ldy modtab,x        ; get base value
.c653	bd e4 ca	        lda lentab,x        ; get bits per digit
.c656	8d 0a 01	        sta numbit          ; store bits per digit
.c659	20 23 c7	nudig   jsr getchr          ; get next char in a
.c65c	f0 63		rdnilk  beq rdnil           ; end of number if no more characters
.c65e	38		        sec
.c65f	e9 30		        sbc #$30            ; subtract ascii value of 0 to get numeric value
.c661	90 5e		        bcc rdnil           ; end of number if character was less than 0
.c663	c9 0a		        cmp #$0a
.c665	90 06		        bcc digmor          ; not a hex digit if less than a
.c667	e9 07		        sbc #$07            ; 7 chars between ascii 9 and a, so subtract 7
.c669	c9 10		        cmp #$10            ; end of number if char is greater than f
.c66b	b0 54		        bcs rdnil
.c66d	8d 09 01	digmor  sta indig           ; store the digit
.c670	cc 09 01	        cpy indig           ; compare base with the digit
.c673	90 4a		        bcc rderr           ; error if the digit >= the base
.c675	f0 48		        beq rderr
.c677	ee 08 01	        inc digcnt          ; increment the number of digits
.c67a	c0 0a		        cpy #10
.c67c	d0 0a		        bne nodecm          ; skip the next part if not using base 10
.c67e	a2 01		        ldx #1
.c680	b5 c1		declp1  lda tmp0,x          ; stash the previous 16-bit value for later use
.c682	9d 0b 01	        sta stash,x
.c685	ca		        dex
.c686	10 f8		        bpl declp1
.c688	ae 0a 01	nodecm  ldx numbit          ; number of bits to shift
.c68b	06 c1		times2  asl tmp0            ; shift 16-bit value by specified number of bits
.c68d	26 c2		        rol tmp0+1
.c68f	b0 2e		        bcs rderr           ; error if we overflowed 16 bits
.c691	ca		        dex
.c692	d0 f7		        bne times2          ; shift remaining bits
.c694	c0 0a		        cpy #10
.c696	d0 18		        bne nodec2          ; skip the next part if not using base 10
.c698	0e 0b 01	        asl stash           ; shift the previous 16-bit value one bit left
.c69b	2e 0c 01	        rol stash+1
.c69e	b0 1f		        bcs rderr           ; error if we overflowed 16 bits
.c6a0	ad 0b 01	        lda stash           ; add shifted previous value to current value
.c6a3	65 c1		        adc tmp0
.c6a5	85 c1		        sta tmp0
.c6a7	ad 0c 01	        lda stash+1
.c6aa	65 c2		        adc tmp0+1
.c6ac	85 c2		        sta tmp0+1
.c6ae	b0 0f		        bcs rderr           ; error if we overflowed 16 bits
.c6b0	18		nodec2  clc
.c6b1	ad 09 01	        lda indig           ; load current digit
.c6b4	65 c1		        adc tmp0            ; add current digit to low byte
.c6b6	85 c1		        sta tmp0            ; and store result back in low byte
.c6b8	8a		        txa                 ; a=0
.c6b9	65 c2		        adc tmp0+1          ; add carry to high byte
.c6bb	85 c2		        sta tmp0+1          ; and store result back in high byte
.c6bd	90 9a		        bcc nudig           ; get next digit if we didn't overflow
.c6bf	38		rderr   sec                 ; set carry to indicate error
>c6c0	24		        .byte $24           ; bit zp opcode consumes next byte (clc)
.c6c1	18		rdnil   clc                 ; clear carry to indicate success
.c6c2	8c 0a 01	        sty numbit          ; save base of number
.c6c5	68		        pla                 ; restore x and y
.c6c6	a8		        tay
.c6c7	68		        pla
.c6c8	aa		        tax
.c6c9	ad 08 01	        lda digcnt          ; return number of digits in a
.c6cc	60		        rts
.c6cd	a5 c3		showad  lda tmp2
.c6cf	a6 c4		        ldx tmp2+1
.c6d1	48		wraddr  pha                 ; save low byte
.c6d2	8a		        txa                 ; put high byte in a
.c6d3	20 fb c6	        jsr wrtwo           ; output high byte
.c6d6	68		        pla                 ; restore low byte
.c6d7	20 fb c6	wrbyte  jsr wrtwo           ; output byte in a
.c6da	a9 20		space   lda #$20            ; output space
.c6dc	d0 0f		        bne flip
.c6de	c9 0d		chout   cmp #$0d            ; output char with special handling of cr
.c6e0	d0 0b		        bne flip
.c6e2	a9 0d		crlf    lda #$0d            ; load cr in a
.c6e4	24 13		        bit $13             ; check default channel
.c6e6	10 05		        bpl flip            ; if high bit is clear output cr only
.c6e8	20 d2 ff	        jsr chrout          ; otherwise output cr+lf
.c6eb	a9 0a		        lda #$0a            ; output lf
.c6ed	4c d2 ff	flip    jmp chrout
.c6f0	20 e2 c6	fresh   jsr crlf            ; output cr
.c6f3	a9 20		        lda #$20            ; load space in a
.c6f5	20 d2 ff	        jsr chrout
.c6f8	4c 79 c9	        jmp snclr
.c6fb	8e 05 01	wrtwo   stx savx            ; save x
.c6fe	20 0b c7	        jsr asctwo          ; get hex chars for byte in x (lower) and a (upper)
.c701	20 d2 ff	        jsr chrout          ; output upper nybble
.c704	8a		        txa                 ; transfer lower to a
.c705	ae 05 01	        ldx savx            ; restore x
.c708	4c d2 ff	        jmp chrout          ; output lower nybble
.c70b	48		asctwo  pha                 ; save byte
.c70c	20 15 c7	        jsr ascii           ; do low nybble
.c70f	aa		        tax                 ; save in x
.c710	68		        pla                 ; restore byte
.c711	4a		        lsr a               ; shift upper nybble down
.c712	4a		        lsr a
.c713	4a		        lsr a
.c714	4a		        lsr a
.c715	29 0f		ascii   and #$0f            ; clear upper nibble
.c717	c9 0a		        cmp #$0a            ; if less than a, skip next step
.c719	90 02		        bcc asc1
.c71b	69 06		        adc #6              ; skip ascii chars between 9 and a
.c71d	69 30		asc1    adc #$30            ; add ascii char 0 to value
.c71f	60		        rts
.c720	ce 31 02	gotchr  dec chrpnt
.c723	8e 05 01	getchr  stx savx
.c726	ae 31 02	        ldx chrpnt          ; get pointer to next char
.c729	bd 00 02	        lda inbuff,x        ; load next char in a
.c72c	f0 06		        beq nochar          ; null, :, or ? signal end of buffer
.c72e	c9 3a		        cmp #":"
.c730	f0 02		        beq nochar
.c732	c9 3f		        cmp #"?"
.c734	08		nochar  php
.c735	ee 31 02	        inc chrpnt          ; next char
.c738	ae 05 01	        ldx savx
.c73b	28		        plp                 ; z flag will signal last character
.c73c	60		        rts
.c73d	a5 c1		copy12  lda tmp0            ; low byte
.c73f	85 c3		        sta tmp2
.c741	a5 c2		        lda tmp0+1          ; high byte
.c743	85 c4		        sta tmp2+1
.c745	60		        rts
.c746	38		sub12   sec
.c747	a5 c1		        lda tmp0            ; subtract low byte
.c749	e5 c3		        sbc tmp2
.c74b	85 c1		        sta tmp0
.c74d	a5 c2		        lda tmp0+1
.c74f	e5 c4		        sbc tmp2+1          ; subtract high byte
.c751	85 c2		        sta tmp0+1
.c753	60		        rts
.c754	a9 01		suba1   lda #1              ; shortcut to decrement by 1
.c756	8d 05 01	suba2   sta savx            ; subtrahend in accumulator
.c759	38		        sec
.c75a	a5 c1		        lda tmp0            ; minuend in low byte
.c75c	ed 05 01	        sbc savx
.c75f	85 c1		        sta tmp0
.c761	a5 c2		        lda tmp0+1          ; borrow from high byte
.c763	e9 00		        sbc #0
.c765	85 c2		        sta tmp0+1
.c767	60		        rts
.c768	38		sub13   sec
.c769	ad 2f 02	        lda store
.c76c	e9 01		        sbc #1              ; decrement low byte
.c76e	8d 2f 02	        sta store
.c771	ad 30 02	        lda store+1
.c774	e9 00		        sbc #0              ; borrow from high byte
.c776	8d 30 02	        sta store+1
.c779	60		        rts
.c77a	a9 01		adda2   lda #1              ; shortcut to increment by 1
.c77c	18		bumpad2 clc
.c77d	65 c3		        adc tmp2            ; add value in accumulator to low byte
.c77f	85 c3		        sta tmp2
.c781	90 02		        bcc bumpex
.c783	e6 c4		        inc tmp2+1          ; carry to high byte
.c785	60		bumpex  rts
.c786	38		sub21   sec
.c787	a5 c3		        lda tmp2            ; decrement low byte
.c789	e9 01		        sbc #1
.c78b	85 c3		        sta tmp2
.c78d	a5 c4		        lda tmp2+1          ; borrow from high byte
.c78f	e9 00		        sbc #0
.c791	85 c4		        sta tmp2+1
.c793	60		        rts
.c794	b0 0a		copy1p  bcs cpy1px          ; do nothing if parameter is empty
.c796	a5 c1		        lda tmp0            ; copy low byte
.c798	a4 c2		        ldy tmp0+1          ; copy high byte
.c79a	8d 29 02	        sta pcl
.c79d	8c 28 02	        sty pch
.c7a0	60		cpy1px  rts
.c7a1	b0 23		getdif  bcs gdifx           ; exit with error if no parameter given
.c7a3	20 3d c7	        jsr copy12          ; save start address in tmp2
.c7a6	20 04 c6	        jsr getpar          ; get end address in tmp0
.c7a9	b0 1b		        bcs gdifx           ; exit with error if no parameter given
.c7ab	a5 c1		        lda tmp0            ; save end address in stash
.c7ad	8d 0b 01	        sta stash
.c7b0	a5 c2		        lda tmp0+1
.c7b2	8d 0c 01	        sta stash+1
.c7b5	20 46 c7	        jsr sub12           ; subtract start address from end address
.c7b8	a5 c1		        lda tmp0
.c7ba	8d 2f 02	        sta store           ; save difference in store
.c7bd	a5 c2		        lda tmp0+1
.c7bf	8d 30 02	        sta store+1
.c7c2	90 02		        bcc gdifx           ; error if start address is after end address
.c7c4	18		        clc                 ; clear carry to indicate success
>c7c5	24		        .byte $24           ; bit zp opcode consumes next byte (sec)
.c7c6	38		gdifx   sec                 ; set carry to indicate error
.c7c7	60		        rts
.c7c8	20 01 c6	convrt  jsr rdpar           ; read a parameter
.c7cb	20 f0 c6	        jsr fresh           ; next line and clear
.c7ce	a9 24		        lda #"$"            ; output $ sigil for hex
.c7d0	20 d2 ff	        jsr chrout
.c7d3	a5 c1		        lda tmp0            ; load the 16-bit value entered
.c7d5	a6 c2		        ldx tmp0+1
.c7d7	20 d1 c6	        jsr wraddr          ; print it in 4 hex digits
.c7da	20 f0 c6	        jsr fresh
.c7dd	a9 2b		        lda #"+"            ; output + sigil for decimal
.c7df	20 d2 ff	        jsr chrout
.c7e2	20 13 c8	        jsr cvtdec          ; convert to bcd using hardware mode
.c7e5	a9 00		        lda #0              ; clear digit counter
.c7e7	a2 06		        ldx #6              ; max digits + 1
.c7e9	a0 03		        ldy #3              ; bits per digit - 1
.c7eb	20 4d c8	        jsr nmprnt          ; print result without leading zeros
.c7ee	20 f0 c6	        jsr fresh           ; next line and clear
.c7f1	a9 26		        lda #"&"            ; print & sigil for octal
.c7f3	20 d2 ff	        jsr chrout
.c7f6	a9 00		        lda #0              ; clear digit counter
.c7f8	a2 08		        ldx #8              ; max digits + 1
.c7fa	a0 02		        ldy #2              ; bits per digit - 1
.c7fc	20 3c c8	        jsr prinum          ; output number
.c7ff	20 f0 c6	        jsr fresh           ; next line and clear
.c802	a9 25		        lda #"%"            ; print % sigil for binary
.c804	20 d2 ff	        jsr chrout
.c807	a9 00		        lda #0              ; clear digit counter
.c809	a2 18		        ldx #$18            ; max digits + 1
.c80b	a0 00		        ldy #0              ; bits per digit - 1
.c80d	20 3c c8	        jsr prinum          ; output number
.c810	4c 66 c0	        jmp strt            ; back to mainloop
.c813	20 3d c7	cvtdec  jsr copy12          ; copy value from tmp0 to tmp2
.c816	a9 00		        lda #0
.c818	a2 02		        ldx #2              ; clear 3 bytes in work buffer
.c81a	9d 0d 01	decml1  sta u0aa0,x
.c81d	ca		        dex
.c81e	10 fa		        bpl decml1
.c820	a0 10		        ldy #16             ; 16 bits in input
.c822	08		        php                 ; save status register
.c823	78		        sei                 ; make sure no interrupts occur with bcd enabled
.c824	f8		        sed
.c825	06 c3		decml2  asl tmp2            ; rotate bytes out of input low byte
.c827	26 c4		        rol tmp2+1          ; .. into high byte and carry bit
.c829	a2 02		        ldx #2              ; process 3 bytes
.c82b	bd 0d 01	decdbl  lda u0aa0,x         ; load current value of byte
.c82e	7d 0d 01	        adc u0aa0,x         ; add it to itself plus the carry bit
.c831	9d 0d 01	        sta u0aa0,x         ; store it back in the same location
.c834	ca		        dex                 ; decrement byte counter
.c835	10 f4		        bpl decdbl          ; loop until all bytes processed
.c837	88		        dey                 ; decrement bit counter
.c838	d0 eb		        bne decml2          ; loop until all bits processed
.c83a	28		        plp                 ; restore processor status
.c83b	60		        rts
.c83c	48		prinum  pha                 ; save accumulator
.c83d	a5 c1		        lda tmp0            ; copy input low byte to work buffer
.c83f	8d 0f 01	        sta u0aa0+2
.c842	a5 c2		        lda tmp0+1          ; copy input high byte to work buffer
.c844	8d 0e 01	        sta u0aa0+1
.c847	a9 00		        lda #0              ; clear overflow byte in work buffer
.c849	8d 0d 01	        sta u0aa0
.c84c	68		        pla                 ; restore accumulator
.c84d	8d 08 01	nmprnt  sta digcnt          ; number of digits in accumulator
.c850	8c 0a 01	        sty numbit          ; bits per digit passed in y register
.c853	ac 0a 01	digout  ldy numbit          ; get bits to process
.c856	a9 00		        lda #0              ; clear accumulator
.c858	0e 0f 01	rolbit  asl u0aa0+2         ; shift bits out of low byte
.c85b	2e 0e 01	        rol u0aa0+1         ; ... into high byte
.c85e	2e 0d 01	        rol u0aa0           ; ... into overflow byte
.c861	2a		        rol a               ; ... into accumulator
.c862	88		        dey                 ; decrement bit counter
.c863	10 f3		        bpl rolbit          ; loop until all bits processed
.c865	a8		        tay                 ; check whether accumulator is 0
.c866	d0 09		        bne nzero           ; if not, print it
.c868	e0 01		        cpx #1              ; have we output the max number of digits?
.c86a	f0 05		        beq nzero           ; if not, print it
.c86c	ac 08 01	        ldy digcnt          ; how many digits have we output?
.c86f	f0 08		        beq zersup          ; skip output if digit is 0
.c871	ee 08 01	nzero   inc digcnt          ; increment digit counter
.c874	09 30		        ora #$30            ; add numeric value to ascii '0' to get ascii char
.c876	20 d2 ff	        jsr chrout          ; output character
.c879	ca		zersup  dex                 ; decrement number of leading zeros
.c87a	d0 d7		        bne digout          ; next digit
.c87c	60		        rts
.c87d	d0 03		dstat   bne chgdev          ; if device address was given, use it
.c87f	a2 08		        ldx #8              ; otherwise, default to 8
>c881	2c		        .byte $2c           ; absolute bit opcode consumes next word (ldx tmp0)
.c882	a6 c1		chgdev  ldx tmp0            ; load device address from parameter
.c884	e0 04		        cpx #4              ; make sure device address is in range 4-31
.c886	90 58		        bcc ioerr
.c888	e0 20		        cpx #32
.c88a	b0 54		        bcs ioerr
.c88c	86 c1		        stx tmp0
.c88e	a9 00		        lda #0              ; clear status
.c890	85 90		        sta satus
.c892	85 b7		        sta fnlen           ; empty filename
.c894	20 23 c7	        jsr getchr          ; get next character
.c897	f0 24		        beq instat1         ; null, display status
.c899	ce 31 02	        dec chrpnt          ; back up 1 char
.c89c	c9 24		        cmp #"$"            ; $, display directory
.c89e	f0 43		        beq direct
.c8a0	a5 c1		        lda tmp0            ; command specified device to listen
.c8a2	20 b1 ff	        jsr listen
.c8a5	a9 6f		        lda #$6f            ; secondary address 15 (only low nybble used)
.c8a7	20 93 ff	        jsr second
.c8aa	ae 31 02	dcomd   ldx chrpnt          ; get next character from buffer
.c8ad	ee 31 02	        inc chrpnt
.c8b0	bd 00 02	        lda inbuff,x
.c8b3	f0 05		        beq instat          ; break out of loop if it's null
.c8b5	20 a8 ff	        jsr ciout           ; otherwise output it to the serial bus
.c8b8	90 f0		        bcc dcomd           ; unconditional loop: ciout clears carry before rts
.c8ba	20 ae ff	instat  jsr unlsn           ; command device to unlisten
.c8bd	20 e2 c6	instat1 jsr crlf            ; new line
.c8c0	a5 c1		        lda tmp0            ; load device address
.c8c2	20 b4 ff	        jsr talk            ; command device to talk
.c8c5	a9 6f		        lda #$6f            ; secondary address 15 (only low nybble used)
.c8c7	20 96 ff	        jsr tksa
.c8ca	20 a5 ff	rdstat  jsr acptr           ; read byte from serial bus
.c8cd	20 d2 ff	        jsr chrout          ; print it
.c8d0	c9 0d		        cmp #$0d            ; if the byte is cr, exit loop
.c8d2	f0 06		        beq dexit
.c8d4	a5 90		        lda satus           ; check status
.c8d6	29 bf		        and #$bf            ; ignore eoi bit
.c8d8	f0 f0		        beq rdstat          ; if no errors, read next byte
.c8da	20 ab ff	dexit   jsr untlk           ; command device to stop talking
.c8dd	4c 66 c0	        jmp strt            ; back to mainloop
.c8e0	4c 95 c0	ioerr   jmp error           ; handle error
.c8e3	a5 c1		direct  lda tmp0            ; load device address
.c8e5	20 b1 ff	        jsr listen          ; command device to listen
.c8e8	a9 f0		        lda #$f0            ; secondary address 0 (only low nybble used)
.c8ea	20 93 ff	        jsr second
.c8ed	ae 31 02	        ldx chrpnt          ; get index of next character
.c8f0	bd 00 02	dir2    lda inbuff,x        ; get next character from buffer
.c8f3	f0 06		        beq dir3            ; break if it's null
.c8f5	20 a8 ff	        jsr ciout           ; send character to device
.c8f8	e8		        inx                 ; increment characer index
.c8f9	d0 f5		        bne dir2            ; loop if it hasn't wrapped to zero
.c8fb	20 ae ff	dir3    jsr unlsn           ; command device to unlisten
.c8fe	20 e2 c6	        jsr crlf            ; new line
.c901	a5 c1		        lda tmp0            ; load device address
.c903	48		        pha                 ; save on stack
.c904	20 b4 ff	        jsr talk            ; command device to talk
.c907	a9 60		        lda #$60            ; secondary address 0 (only low nybble used)
.c909	20 96 ff	        jsr tksa
.c90c	a0 03		        ldy #3              ; read 3 16-bit values from device
.c90e	8c 2f 02	dirlin  sty store           ;   ignore the first 2; 3rd is file size
.c911	20 a5 ff	dlink   jsr acptr           ; read low byte from device
.c914	85 c1		        sta tmp0            ; store it
.c916	a5 90		        lda satus           ; check status
.c918	d0 44		        bne drexit          ; exit if error or eof occurred
.c91a	20 a5 ff	        jsr acptr           ; read high byte from device
.c91d	85 c2		        sta tmp0+1          ; store it
.c91f	a5 90		        lda satus           ; check status
.c921	d0 3b		        bne drexit          ; exit if error or eof cocurred
.c923	ce 2f 02	        dec store           ; decrement byte count
.c926	d0 e9		        bne dlink           ; loop if bytes remain
.c928	20 13 c8	        jsr cvtdec          ; convert last 16-bit value to decimal
.c92b	a9 00		        lda #0              ; clear digit count
.c92d	a2 06		        ldx #6              ; max 6 digits
.c92f	a0 03		        ldy #3              ; 3 bits per digit
.c931	20 4d c8	        jsr nmprnt          ; output number
.c934	a9 20		        lda #" "            ; output space
.c936	20 d2 ff	        jsr chrout
.c939	20 a5 ff	dname   jsr acptr           ; get a filename character from the device
.c93c	f0 0a		        beq dmore           ; if it's null, break out of loop
.c93e	a6 90		        ldx satus           ; check for errors or eof
.c940	d0 1c		        bne drexit          ; if found exit early
.c942	20 d2 ff	        jsr chrout          ; output character
.c945	18		        clc
.c946	90 f1		        bcc dname           ; unconditional branch to read next char
.c948	20 e2 c6	dmore   jsr crlf
.c94b	20 e1 ff	        jsr stop            ; check for stop key
.c94e	f0 0e		        beq drexit          ; exit early if pressed
.c950	20 e4 ff	        jsr getin           ; pause if a key was pressed
.c953	f0 05		        beq nopaws
.c955	20 e4 ff	paws    jsr getin           ; wait until another key is pressed
.c958	f0 fb		        beq paws
.c95a	a0 02		nopaws  ldy #2
.c95c	d0 b0		        bne dirlin          ; unconditional branch to read next file
.c95e	20 ab ff	drexit  jsr untlk           ; command device to untalk
.c961	68		        pla                 ; restore accumulator
.c962	20 b1 ff	        jsr listen          ; command device to listen
.c965	a9 e0		        lda #$e0            ; secondary address 0 (only low nybble is used)
.c967	20 93 ff	        jsr second
.c96a	20 ae ff	        jsr unlsn           ; command device to unlisten
.c96d	4c 66 c0	        jmp strt            ; back to mainloop
.c970	20 e2 c6	cline   jsr crlf            ; send cr+lf
.c973	4c 79 c9	        jmp snclr           ; clear line
.c976	20 89 c9	sndclr  jsr sndmsg
.c979	a0 28		snclr   ldy #$28            ; loop 40 times
.c97b	a9 20		snclp   lda #$20            ; output space character
.c97d	20 d2 ff	        jsr chrout
.c980	a9 14		        lda #$14            ; output delete character
.c982	20 d2 ff	        jsr chrout
.c985	88		        dey
.c986	d0 f3		        bne snclp
.c988	60		        rts
.c989	b9 97 c9	sndmsg  lda msgbas,y        ; y contains offset in msg table
.c98c	08		        php
.c98d	29 7f		        and #$7f            ; strip high bit before output
.c98f	20 de c6	        jsr chout
.c992	c8		        iny
.c993	28		        plp
.c994	10 f3		        bpl sndmsg          ; loop until high bit is set
.c996	60		        rts
=$c997			msgbas  =*
>c997	0d		msg2    .byte $0d               ; header for registers
>c998	20 20 20 50	        .text "   pc  sr ac xr yr sp   v1.2"
>c99c	43 20 20 53 52 20 41 43 20 58 52 20 59 52 20 53
>c9ac	50 20 20 20 56 31 2e 32
>c9b4	8d		        .byte $0d+$80
>c9b5	1d bf		msg3    .byte $1d,$3f+$80       ; syntax error: move right, display "?"
>c9b7	2e 2e 53 59	msg4    .text "..sys"           ; sys call to enter monitor
>c9bb	53
>c9bc	a0		        .byte $20+$80
>c9bd	3a 92		msg5    .byte $3a,$12+$80       ; ":" then rvs on for memory ascii dump
>c9bf	20 45 52 52	msg6    .text " erro"           ; i/o error: display " error"
>c9c3	4f
>c9c4	d2		        .byte "r"+$80
>c9c5	41 a0		msg7    .byte $41,$20+$80       ; assemble next instruction: "a " + addr
>c9c7	20 20		msg8    .text "  "              ; pad non-existent byte: skip 3 spaces
>c9c9	a0		        .byte $20+$80
>c9ca	40 02 45 03	mode    .byte $40,$02,$45,$03   ; even opcodes
>c9ce	d0 08 40 09	        .byte $d0,$08,$40,$09
>c9d2	30 22 45 33	        .byte $30,$22,$45,$33
>c9d6	d0 08 40 09	        .byte $d0,$08,$40,$09
>c9da	40 02 45 33	        .byte $40,$02,$45,$33
>c9de	d0 08 40 09	        .byte $d0,$08,$40,$09
>c9e2	40 02 45 b3	        .byte $40,$02,$45,$b3
>c9e6	d0 08 40 09	        .byte $d0,$08,$40,$09
>c9ea	00 22 44 33	        .byte $00,$22,$44,$33
>c9ee	d0 8c 44 00	        .byte $d0,$8c,$44,$00
>c9f2	11 22 44 33	        .byte $11,$22,$44,$33
>c9f6	d0 8c 44 9a	        .byte $d0,$8c,$44,$9a
>c9fa	10 22 44 33	        .byte $10,$22,$44,$33
>c9fe	d0 08 40 09	        .byte $d0,$08,$40,$09
>ca02	10 22 44 33	        .byte $10,$22,$44,$33
>ca06	d0 08 40 09	        .byte $d0,$08,$40,$09
>ca0a	62 13 78 a9	        .byte $62,$13,$78,$a9   ; opcodes ending in 01
>ca0e	00		mode2   .byte $00   ; 000 000    00                  0   error
>ca0f	21		        .byte $21   ; 001 000    01      #$00        1   immediate
>ca10	81		        .byte $81   ; 100 000    01      $00         2   zero-page
>ca11	82		        .byte $82   ; 100 000    10      $0000       3   absolute
>ca12	00		        .byte $00   ; 000 000    00                  4   implied
>ca13	00		        .byte $00   ; 000 000    00                  5   accumulator
>ca14	59		        .byte $59   ; 010 110    01      ($00,x)     6   indirect,x
>ca15	4d		        .byte $4d   ; 010 011    01      ($00),y     7   indirect,y
>ca16	91		        .byte $91   ; 100 100    01      $00,x       8   zero-page,x
>ca17	92		        .byte $92   ; 100 100    10      $0000,x     9   absolute,x
>ca18	86		        .byte $86   ; 100 001    10      $0000,y     a   absolute,y
>ca19	4a		        .byte $4a   ; 010 010    10      ($0000)     b   indirect
>ca1a	85		        .byte $85   ; 100 001    01      $00,y       c   zero-page,y
>ca1b	9d		        .byte $9d   ; 100 111    01      $0000*      d   relative
>ca1c	2c 29 2c	char1   .byte $2c,$29,$2c       ; ","  ")"  ","
>ca1f	23 28 24	        .byte $23,$28,$24       ; "#"  "("  "$"
>ca22	59 00 58	char2   .byte $59,$00,$58       ; "y"   0   "x"
>ca25	24 24 00	        .byte $24,$24,$00       ; "$"  "$"   0
>ca28	1c 8a 1c 23	mneml   .byte $1c,$8a,$1c,$23   ; brk php bpl clc
>ca2c	5d 8b 1b a1	        .byte $5d,$8b,$1b,$a1   ; jsr plp bmi sec
>ca30	9d 8a 1d 23	        .byte $9d,$8a,$1d,$23   ; rti pha bvc cli
>ca34	9d 8b 1d a1	        .byte $9d,$8b,$1d,$a1   ; rts pla bvs sei
>ca38	00 29 19 ae	        .byte $00,$29,$19,$ae   ; ??? dey bcc tya
>ca3c	69 a8 19 23	        .byte $69,$a8,$19,$23   ; ldy tay bcs clv
>ca40	24 53 1b 23	        .byte $24,$53,$1b,$23   ; cpy iny bne cld
>ca44	24 53 19 a1	        .byte $24,$53,$19,$a1   ; cpx inx beq sed
>ca48	00 1a 5b 5b	        .byte $00,$1a,$5b,$5b   ; ??? bit jmp jmp
>ca4c	a5 69 24 24	        .byte $a5,$69,$24,$24   ; sty ldy cpy cpx
>ca50	ae ae a8 ad	        .byte $ae,$ae,$a8,$ad   ; txa txs tax tsx
>ca54	29 00 7c 00	        .byte $29,$00,$7c,$00   ; dex ??? nop ???
>ca58	15 9c 6d 9c	        .byte $15,$9c,$6d,$9c   ; asl rol lsr ror
>ca5c	a5 69 29 53	        .byte $a5,$69,$29,$53   ; stx ldx dec inc
>ca60	84 13 34 11	        .byte $84,$13,$34,$11   ; ora and eor adc
>ca64	a5 69 23 a0	        .byte $a5,$69,$23,$a0   ; sta lda cmp sbc
>ca68	d8 62 5a 48	mnemr   .byte $d8,$62,$5a,$48   ; brk php bpl clc
>ca6c	26 62 94 88	        .byte $26,$62,$94,$88   ; jsr plp bmi sec
>ca70	54 44 c8 54	        .byte $54,$44,$c8,$54   ; rti pha bvc cli
>ca74	68 44 e8 94	        .byte $68,$44,$e8,$94   ; rts pla bvs sei
>ca78	00 b4 08 84	        .byte $00,$b4,$08,$84   ; ??? dey bcc tya
>ca7c	74 b4 28 6e	        .byte $74,$b4,$28,$6e   ; ldy tay bcs clv
>ca80	74 f4 cc 4a	        .byte $74,$f4,$cc,$4a   ; cpy iny bne cld
>ca84	72 f2 a4 8a	        .byte $72,$f2,$a4,$8a   ; cpx inx beq sed
>ca88	00 aa a2 a2	        .byte $00,$aa,$a2,$a2   ; ??? bit jmp jmp
>ca8c	74 74 74 72	        .byte $74,$74,$74,$72   ; sty ldy cpy cpx
>ca90	44 68 b2 32	        .byte $44,$68,$b2,$32   ; txa txs tax tsx
>ca94	b2 00 22 00	        .byte $b2,$00,$22,$00   ; dex ??? nop ???
>ca98	1a 1a 26 26	        .byte $1a,$1a,$26,$26   ; asl rol lsr ror
>ca9c	72 72 88 c8	        .byte $72,$72,$88,$c8   ; stx ldx dec inc
>caa0	c4 ca 26 48	        .byte $c4,$ca,$26,$48   ; ora and eor adc
>caa4	44 44 a2 c8	        .byte $44,$44,$a2,$c8   ; sta lda cmp sbc
>caa8	0d 20 20 20	        .byte $0d,$20,$20,$20
>caac	41 43 44 46	keyw    .text "acdfghjmrtx@.>;"
>cab0	47 48 4a 4d 52 54 58 40 2e 3e 3b
>cabb	24 2b 26 25	hikey   .text "$+&%lsv"
>cabf	4c 53 56
=$cac2			keytop  =*
>cac2	48 c3 a0 c1	kaddr   .word assem-1,compar-1,disass-1,fill-1
>cac6	d5 c4 23 c3
>caca	2b c1 24 c2	        .word goto-1,hunt-1,jsub-1,dsplym-1
>cace	49 c1 be c0
>cad2	43 c0 a3 c1	        .word dsplyr-1,trans-1,exit-1,dstat-1
>cad6	bb c0 7c c8
>cada	48 c3 0b c1	        .word assem-1,altm-1,altr-1
>cade	f4 c0
>cae0	10 0a 08 02	modtab  .byte $10,$0a,$08,02    ; modulo number systems
>cae4	04 03 03 01	lentab  .byte $04,$03,$03,$01   ; bits per digit
>cae8	35 c0		linkad  .word break             ; address of brk handler
>caea	00 c0		supad   .word super             ; address of entry point

;******  End of listing
